# XS 語法教科書：從入門到實戰

> [!NOTE]
> 本文集結了 XS 語言的核心開發邏輯、實戰案例與系統行為說明，是您開發自動化交易策略的最佳指引。

## 📚 技術索引

### 基礎觀念與語法控制
- [GetField 預設值](#GetField 預設值)
- [取得資料欄位時的「對位問題」](#取得資料欄位時的「對位問題」)
- [台股逐筆撮合的連續成交Tick序列](#台股逐筆撮合的連續成交Tick序列)
- [計算區間漲跌幅的自訂函數](#計算區間漲跌幅的自訂函數)
- [選股欄位放大鏡：談OutputField跟GetFieldDate這兩個函數](#選股欄位放大鏡：談OutputField跟GetFieldDate這兩個函數)
- [探討變數序列的觀念：幾天前黃金交叉商品為例](#探討變數序列的觀念：幾天前黃金交叉商品為例)
- [SetBackBar指定頻率設定資料筆數](#SetBackBar指定頻率設定資料筆數)
- [SetTotalBar資料讀取範圍與腳本執行的關係](#SetTotalBar資料讀取範圍與腳本執行的關係)

### 選股策略與籌碼分析
- [選股中心創掛牌新高與大單欄位的應用](#選股中心創掛牌新高與大單欄位的應用)
- [用XS寫籌碼集中度的選股策略](#用XS寫籌碼集中度的選股策略)

### 指標與繪圖視覺化
- [關於背離的寫法](#關於背離的寫法)

### 交易策略與盤中洗價
- [Tick欄位的應用](#Tick欄位的應用)
- [私房交易策略之：強勢股整理結束](#私房交易策略之：強勢股整理結束)

### 進階技巧與除錯
- [排行語法](#排行語法)
- [商品清單功能](#商品清單功能)
- [XS美股新增欄位列表](#XS美股新增欄位列表)
- [XS美股欄位的使用介紹](#XS美股欄位的使用介紹)
- [盤中即時資料欄位的應用](#盤中即時資料欄位的應用)
- [利用InputKind函數製作跨頻率週期的選取介面](#利用InputKind函數製作跨頻率週期的選取介面)
- [利用GetSymbolInfo函數計算選擇權的希臘字母Delta](#利用GetSymbolInfo函數計算選擇權的希臘字母Delta)
- [那些股票中長紅之後還會續漲?](#那些股票中長紅之後還會續漲?)
- [如何運用Print指令來抓程式的臭蟲](#如何運用Print指令來抓程式的臭蟲)

---

<a name="排行語法"></a>
<a name="排行語法"></a>
# [排行語法](https://www.xq.com.tw/lesson/xspractice/%E6%8E%92%E8%A1%8C%E8%AA%9E%E6%B3%95/)

語法
基礎範例

```xs
rank myRank begin
Value1 = Average(Close, 10);
retval = (Close - Value1);
end;

if myRank.pos <= 100 then ret = 1;
OutputField1(myRank.value);
```

上面範例中，使用了 rank 宣告了 myRank 的物件，並將收盤價和移動平均線的差距來當作排行的條件。 接著透過呼叫 myRank 物件的 pos 屬性，將前一百名的商品篩選出，並使用 OutputField 將符合條件商品的收盤價和移動平均線差距輸出。

rank 語法只支援選股腳本，其他的腳本都無法使用。

每個排行透過 rank 函數宣告排行物件，並在 begin 到 end 區塊內撰寫排行條件所需的計算數值，最後透過 retval 回傳作為排行依據的數值，預設為由大到小。 當 rank 物件運算完成後，即可使用其屬性來進行排序與篩選等相關操作。

以下為 rank 物件支援的屬性：
屬性名稱說明pos排行名次，整數，從 1 開始，1 是第一名。range排行 %，等於 pos / <參與排行商品數> * 100。這是一個實數 (有小數點)，數值範圍介於 0 ~ 100 之間，越小排名越前面。prPercentile Rank %。PR = (N - pos) / (N - 1) * 100。這是一個實數，數值範圍介於 100 ~ 0 之間，第一名是 100，PR95 表示是 95% 之前。count參與排行的商品個數。這個數值對任何一檔商品而言都是固定的。valuerank object 的回傳數值，也就是 retval 的回傳數值。avgvalue所有商品 rank.value 的平均值。可以使用這個數值來決定目前商品是否是在平均值以上或是平均值以下。這個數值對任何一檔商品而言都是固定的。medvalue所有商品 rank.value 的中位數 (median value)。可以使用這個數值來決定目前商品是否是在中位數以上或是中位數以下。這個數值對任何一檔商品而言都是固定的。rank 語法的注意點
一、同個腳本當中可以有多個 rank 物件，但每個物件的名稱不能和彼此、腳本中變數以及參數重複。

(X) 錯誤的案例：

```xs
var: myRank(0);
rank myRank begin
Value1 = Average(Close, 10);
retval = (Close - Value1);
end;

(O) 正確的案例：

rank myRank1 begin
retval = Close;
end;
rank myRank2 begin
retval = Volume;
end;

```

二、rank 物件不能夠放在任何的條件或是 begin … end 之間，需要放在最上面一層。

(X) 錯誤的案例：

```xs
if close > open then begin
rank myRank begin
Value1 = Average(Close, 10);
retval = (Close - Value1);
end;
end;

```

排行的結果是一個序列。

```xs
rank myRank begin
retval = Close;
end;
if myRank.pos[1] <> myRank.pos then ret = 1;
//篩選出前期排行與當期排行不同的商品

```

三、rank 排行預設為大到小，但可以透過 desc 和 asc 來設定是大到小還是小到大。

```xs
//大到小
rank myRank1 desc begin
retval = Close;
end;

//小到大
rank myRank2 asc begin
retval = Close;
end;

```

四、rank 物件內是獨立空間，無法傳入腳本他處的參數和變數。

rank 物件使用的變數需另行在 rank 區間內宣告 (亦可使用內建的 Value1 等變數)。 變數即使有相同的名字，但不會互相影響 (內建變數亦相同)。(X) 錯誤的案例：

```xs
//錯誤訊息要特別註明
Input: len(10);
rank myRank begin
Value1 = Average(Close, len);
retval = (Close - Value1);
end;

(O) 正確的案例：
//編譯時禁止 rank 內宣告相同名稱的變數
var: _price(0);
_price = Open;
rank myRank begin
var: _price(0);
_price = Average(Close, 10);
retval = (Close - _price);
end;
// rank 物件內 _price 會是 10 期收盤價移動平均值，物件外的 _price 會是開盤價

```

五、rank 內雖然不能夠使用外部的參數變數，但可以使用前面其他 rank 運算出來的結果。

舉例來說，在 Alpha 101 的因子內，有些因子會需要針對某些排行結果再做另外一次排行。其中 Alpha 13＝-1 * rank(covariance(rank(close), rank(volume), 5))，這個因子的邏輯是：

首先統計過去 5 日每一日 close 在母體內排名以及每一日 volume 在母體內的排名。 如果這 5 日的排名正相關的話 (>1)，那就賣出這檔股票，如果是負相關的話 (<-1)，那就買進這檔股票。注意到這個因子必須針對過去 5 日的排行數值的運算結果 (covariance) 再做一次排行。以下是模擬 Alpha 13 的腳本範例：

```xs
//收盤價排行

rank rank_close begin
retval = close;
end;

//成交量排行

rank rank_volume begin
retval = volume;
end;

//上述兩個排行的相關性

rank rank_alpha_13 begin
retval = Covariance(rank_close.pr, rank_volume.pr, 5);
end;
ret = 1;

OutputField1(rank_alpha_13.value, "相關性"); |
```

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="商品清單功能"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="商品清單功能"></a>
# [商品清單功能](https://www.xq.com.tw/lesson/xspractice/%E5%95%86%E5%93%81%E6%B8%85%E5%96%AE%E5%8A%9F%E8%83%BD/)

此次更新新增了商品清單功能，使用者可在 Input 參數中設定所需的商品清單，或透過新建立的 Group 語法獲取指定商品的成分股、期貨或可轉債等商品清單，並搭配 GetSymbolField 快速取得所需資訊。

語法說明
透過 Input 宣告清單

```xs
Input: myGroup(Group);
Var: _TF(False);

Value1 = GetSymbolField(myGroup[1], "Close", "D");
_TF = GetSymbolInfo(myGroup[1], "可放空");

```

上述範例使用 Input 語法宣告了一個清單 myGroup ，允許在指標或策略中設定所需的商品清單 。 myGroup 是一個陣列，存放清單中的商品代號，並可透過 [N] 來取得陣列中第 N 個商品代號 。 GetSymbolField 和 GetSymbolInfo 在這次修改中增加支援 Group 清單的功能 （ 需注意還是無法使用變數指定商品代碼 ） 。

從 Input 宣告的 Group 有四種方法可以設定清單 ：

商品： 使用者自行輸入所需商品 組合： 使用者選擇所需的組合 （ 包含自選股 ） 選股： 使用者選擇一個選股中心策略 （ 選股中心策略不能選擇其他選股中心的策略 ） 檔案： 使用者選擇一個外部傳入的檔案 （ 支援 .txt 和 .csv ）

以上四種設定方式與目前策略雷達和自動交易中心中設定執行商品的方式相同 。
透過 Group 宣告清單

```xs
Group: myGroup();
Var: _TF(False);

myGroup = GetSymbolGroup("TSE23.TW", "成分股");

Value1 = GetSymbolField(myGroup[1], "Close", "D");
_TF = GetSymbolInfo(myGroup[1], "可放空");

```

上述範例使用 Group 語法宣告了一個清單 myGroup ，接著再透過 GetSymbolGroup 來取得指定商品的成分股 。

GetSymbolGroup 可用來取得系統內建的商品清單 。

第一個參數指定要取得清單的商品，例如範例中的 TSE23.TW 。 若未指定商品，則預設取得目前執行商品的清單 （ 前提是該清單存在 ） 。 第二個參數指定所需的清單類型，例如範例中的 成分股 ，此參數為必填 。以下為支援的清單類型 ：
清單類型支援商品說明成分股指數商品、細產業商品回傳指數 ／ 細產業指數的成分股清單。

支援台灣市場。

指數的範圍為交易所公佈的類股指數，以及XQ所編制的細產業指數 (包含族群)，例如TSE11.TW。

不包含TSE.TW、OTC.TW這種全市場的指數。權證股票回傳此商品的所有權證商品。

支援台股。股期指數 ／ 股票回傳此商品的所有期貨。

支援台灣市場。可轉債股票回傳此商品的所有可轉債商品。

支援台股。選擇權指數 ／ 股票回傳此商品的所有選擇權。

支援台灣市場。(註： 在走勢圖 ／ 技術線圖上的商品名稱點擊右鍵，可確認該商品支援的相關清單類型 )
指標腳本的清單選單

```xs
Input:cb_id("", inputKind:=SymbolGroup("CB"), quickedit:=true);
value1 = GetSymbolField(cb_id, "Close");
value2 = GetSymbolField(cb_id, "Volume");
plot1(value1, "CB走勢");
plot2(value2, "CB成交量");

```

在指標腳本中，若希望使用者能在介面上直接選擇所需的清單商品，可以在 Input 宣告時一併傳入清單類型 。

這樣一來，在技術分析頁面中，使用者即可透過選單快速切換執行商品對應的清單類型 。 例如上述範例中，若執行商品對應到多個可轉債，使用者便能透過選單切換，輕鬆繪製可轉債的價格與成交量 。

搭配清單使用的函數

```xs
Value1 = GroupSize(myGroup);

```

GroupSize 會回傳商品清單包含的數量 。 可以此數值避免取用到超出陣列範圍的資料而導致錯誤 。
使用範例
盤中漲幅排行 [警示腳本]

```xs
input: _S(group, "排行股票");
Array: rankRT[2000, 2](-9999);

SetTotalBar(10);
value1 = GroupSize(_S);

//將清單的 商品代碼 以及 漲跌幅 的數值放入陣列中

for value2 = 1 to value1 begin
value3 = 100 * (getsymbolField(_S[value2], "Close", default = 0)
- getsymbolField(_S[value2], "參考價", "D", default = 0))
/ getsymbolField(_S[value2], "參考價", "D", default = 1);
rankRT[value2, 1] = strtonum(leftStr(_S[value2], 4));
rankRT[value2, 2] = value3;
end;

//將陣列依據漲跌幅排序後印出
Array_Sort2d(rankRT, 1, value1, 2, false);
print("==========漲跌幅前10==========");
print("日期時間：", numtostr(datetime, 0));
for value2 = 1 to 10 begin
print(text("排名", numtostr(value2, 0), "商品: "),
text(numtostr(rankRT[value2, 1], 0), ".TW"),
" / 漲跌幅: ", rankRT[value2, 2]);
end;

print("==============================");
if symbol = text(numtostr(rankRT[1, 1], 0), ".TW") then ret = 1;

```

上述腳本中宣告了一個清單 （ 設定為需要盤中即時排序的商品 ） 和陣列，並將清單內的商品代號和漲跌幅存入陣列後進行排序 。
指數成分股的營收加總 [指標腳本]

```xs
group: _symbolGroup();
var: _sum(0), _num(0);

_symbolGroup = GetSymbolGroup("成分股");
value1 = GroupSize(_symbolGroup);

_sum = 0;
_num = 0;
for value2 = 1 to value1 begin
if CheckSymbolField(_symbolGroup[value2], "月營收", "M") then begin
_sum += GetSymbolField(_symbolGroup[value2], "月營收", "M");
_num += 1;
end;
end;

plot1(_sum);
SetPlotLabel(1, "成分股月營收");
plot2(_num);
SetPlotLabel(2, "有月營收家數");
plot3(value1);
SetPlotLabel(3, "成分股家數");

```

使用者可將執行商品設為指數商品 （ 如 TSE23.TW ） ，並透過 _symbolGroup 取得該指數對應的成分股清單 。

接著使用 CheckSymbolField 判斷指定成分股的月營收欄位是否存在，加總月營收和計算有資料的成分股數量，最後將其畫出 。
對應選擇權的成交金額加總 [指標腳本]

```xs
group: _list();
var: _uSum(0), _dSum(0), _count(0), _cp("");

_list = getsymbolgroup("選擇權");

_uSum = 0; //買權加總
_dSum = 0; //賣權加總
_count = 0;
for value1 = 1 to groupSize(_list) begin
if getsymbolField(_list[value1], "Date", "Tick", default:= 0) = getfield("Date", default:= -999999) then begin
_cp = getsymbolinfo(_list[value1], "買賣權");
if _cp = "CALL" then _uSum += getsymbolField(_list[value1], "Close", "Tick", default:=0) * getsymbolField(_list[value1], "Volume", "Tick", default:=0) * 2000;
if _cp = "PUT" then _dSum += getsymbolField(_list[value1], "Close", "Tick", default:=0) * getsymbolField(_list[value1], "Volume", "Tick", default:=0) * 2000;
end;
end;

plot1(_uSum, "近一筆買權權利金加總金額");
plot2(_dSum, "近一筆賣權權利金加總金額");

```

這個腳本會加總執行商品該日最近一筆買進權利金的交易金額 （ 買權賣權分開加總 ） ，並將這些總額繪製出來 。 透過觀察這個總額的變化，可以推測市場對該執行商品價格漲跌的看法，就像「以權追股」的功能一樣 。

如果想要根據成交量判斷是否有大額交易，或者只加總最近一段時間的成交金額，可以在腳本中的 For 迴圈部分進行修改 。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="GetField 預設值"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="GetField 預設值"></a>
# [GetField 預設值](https://www.xq.com.tw/lesson/xspractice/getfield-%E9%A0%90%E8%A8%AD%E5%80%BC/)

語法說明
default

```xs
Value1 = GetField("大戶持股比例", "W", default := 0);
Value2 = GetSymbolField("2330.TW", "大戶持股比例", "W", default := Value2[1]);
```

GetField / GetSymbolField 在傳入參數時，現在可額外加上 default 參數。 此參數可以是固定數值 （ 如上方範例 0 ） 或是變數 （ 如上方範例 Value2[1] ）。

當欄位資訊輸入正確時，以下情境支援預設值：
資料尚未更新。 資料中間有空值。 資料讀取範圍內沒有資料。 資料序列為空。 例如： 從未發生不定期欄位事件的商品。以下情境則不支援預設值：
取未來值。 回測頻率為 1 分鐘逐筆洗價取 Tick 或是分鐘頻率資料欄位，因無法對位故會發生錯誤。 不支援的商品 ／ 頻率。 回測範圍內沒有發生過不定期欄位的事件。 （ 會回最新一筆 ）
CheckField

```xs
CheckField("外盤量", "D");
CheckSymbolField("TSE.TW", "外盤量");

```

CheckField / CheckSymbolField 會依據傳入的商品代碼、欄位和頻率來判斷該資料是否能夠取用，回傳 True / False。

IsSupportField

```xs
IsSupportField("月營收", "M");
IsSupportSymbolField("TSE.TW", "月營收");

```

IsSupportField / IsSupportSymbolField 可根據傳入的商品代碼、欄位和頻率，判斷指定的欄位是否存在，回傳 True / False。

需注意此函數並不會判斷對應 K 棒上是否有資料，只會判斷欄位是否存在。

因此，可能會發生函數回傳 True ，但該根 K 棒上沒有資料的情況。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="XS美股新增欄位列表"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="XS美股新增欄位列表"></a>
# [XS美股新增欄位列表](https://www.xq.com.tw/lesson/xspractice/xs%E7%BE%8E%E8%82%A1%E6%96%B0%E5%A2%9E%E6%AC%84%E4%BD%8D%E5%88%97%E8%A1%A8/)

7.12.01 / 3.12.01 版本，新增支援美國市場的 XS 欄位列表，分為「選股、資料、商品資訊欄位」以表格的方式呈列，

支援美國市場的選股欄位，此次有 71 個如下表：
欄位分類選股欄位名稱可用頻率支援商品常用、價格本益比日美(股票)常用、價格股價淨值比日美(股票)常用、基本殖利率日美(股票)、美(特別股)、美(ETF)常用、財務營業毛利率季、年美(股票)常用、財務股東權益報酬率季、年美(股票)常用、財務每股稅後淨利(元)季、年美(股票)價格貝他值日美(ETF)價格SHARPE日美(ETF)價格Treynor日美(ETF)價格Jensen日美(ETF)價格1年標準差日美(ETF)價格3年標準差日美(ETF)價格5年標準差日美(ETF)價格10年標準差日美(ETF)籌碼ETF規模月美(ETF)籌碼機構持股季美(股票)籌碼機構持股比重季美(股票)籌碼內部人持股異動日、週、月美(股票)籌碼內部人持股日、週、月美(股票)基本投資建議評級最新美(股票)基本員工人數季、年美(股票)基本現金股利年美(股票)、美(特別股)、美(ETF)基本公司成立日期最新美(ETF)基本總市值(元)日美(股票)財務流動資產季、年美(股票)財務長期投資季、年美(股票)財務固定資產季、年美(股票)財務無形資產季、年美(股票)財務資產總額季、年美(股票)財務流動負債季、年美(股票)財務負債總額季、年美(股票)財務普通股股本季、年美(股票)財務股東權益總額季、年美(股票)財務營業收入淨額季、年美(股票)財務營業成本季、年美(股票)財務營業毛利季、年美(股票)財務營業利益季、年美(股票)財務稅前淨利季、年美(股票)財務本期稅後淨利季、年美(股票)財務加權平均股數季、年美(股票)財務稅前息前折舊前淨利季、年美(股票)財務營業利益率季、年美(股票)財務稅前淨利率季、年美(股票)財務稅後淨利率季、年美(股票)財務每股淨值(元)季、年美(股票)財務資產報酬率季、年美(股票)財務流動比率季、年美(股票)財務速動比率季、年美(股票)財務負債比率季、年美(股票)財務投資活動之現金流量季、年美(股票)財務來自營運之現金流量季、年美(股票)財務理財活動之現金流量季、年美(股票)財務自由現金流量營收比季、年美(股票)財務營運資金季、年美(股票)財務當期財報截止日季、年美(股票)財務總流通在外股數季、年美(股票)財務EPS預估值季美(股票)財務EPS法說公佈值季美(股票)財務稀釋後每股淨利季、年美(股票)財務年報酬率最新美(ETF)財務1年年化報酬率日年報酬率財務3年年化報酬率日年報酬率財務5年年化報酬率日年報酬率財務10年年化報酬率日年報酬率事件日期除權日期最新美(股票)事件日期除息日期最新美(股票)事件日期股利年度年美(股票)、美(ETF)事件日期除權年度最新美(股票)事件日期除息年度最新美(股票)事件日期除息值最新美(股票)事件日期除權值最新美(股票)支援美國市場的資料欄位，此次有 9 個如下表：
欄位分類資料欄位名稱可用頻率支援商品量能總成交次數分鐘、日美(股票)籌碼機構持股季美(股票)籌碼內部人持股異動日、週、月美(股票)籌碼內部人持股日、週、月美(股票)籌碼機構持股比重季美(股票)基本本益比日美(股票)基本殖利率日美(股票)、美(特別股)、美(ETF)基本投資建議評級最新美(股票)支援美國市場的商品資訊欄位，此次有 7 個如下表：
商品資訊欄位名稱可用頻率支援商品交易所最新美(股票)ETF分類最新美(ETF)到期日最新美(特別股)票面利率最新美(特別股)面額最新美(特別股)ETD最新美(特別股)第一個回購日最新美(特別股)以上是「美股新增欄位列表」為付費功能，整理成表格的方式，讓大家可以快速審視有哪些欄位有支援美國市場，希望有幫助到大家，感謝大家耐心的收看??

延伸運用：
XS美股欄位的使用介紹 進一步了解美股分析模組 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="XS美股欄位的使用介紹"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="XS美股欄位的使用介紹"></a>
# [XS美股欄位的使用介紹](https://www.xq.com.tw/lesson/xspractice/xs%E7%BE%8E%E8%82%A1%E6%AC%84%E4%BD%8D%E7%9A%84%E4%BD%BF%E7%94%A8%E4%BB%8B%E7%B4%B9/)

在 7.12.01 / 3.12.01 版本（簡稱 *.12.01）XS 將新增 71 個美股選股欄位與 9 個美股資料欄位，讓大家可以獲取更多美股資訊，進行更多元的投資策略佈局。

大家可以參考「美股新增欄位列表」文章介紹在 *.12.01 總共新增了哪些美股的選股 / 資料欄位。

而此篇文章介紹用 XS 取得「基本面／財報／籌碼」這三種分類，常見的美股欄位資訊，希望大家看過此篇文章，能得到一些靈感，來構築投資美股的決勝策略。
基本面欄位
美股基本面的XS選股欄位，在 *.12.01 版本有
殖利率 總市值(元) 現金股利我們可以使用「殖利率」選股欄位，製作美股市場的「殖利率」排行榜。

並用「現金股利」檢視美股的配息數值，再用「總市值(元)」來檢視美股商品的價值大小，如下圖

接下來將一步一步指導大家如何做出美股「殖利率」排行榜。

第一步：在 XS 編輯器，新增「_YieldRank」函數腳本，程式碼如下

```xs
Retval = GetField("殖利率", "D");
```

第二步：在 XS 編輯器，新增「殖利率相關資訊」選股腳本，程式碼如下

```xs
Ret = 1;
outputField1(GetField("殖利率", "D"),"殖利率");
outputField2(GetFielddate("殖利率", "D"),"殖利率資料日期");
outputField3(GetField("現金股利", "Y"),"現金股利");
outputField4(GetFielddate("現金股利", "Y"),"現金股利資料日期");
outputField5(GetField("總市值(元)", "D"),"總市值(元)");
outputField6(GetFielddate("總市值(元)", "D"),"總市值(元)資料日期");
```

第三步：在選股中心，新增「標普500_殖利率排行」策略，並參考下圖，依以下指示操作。

1. 市場別：請設定為「美股」 2. 範圍：請設定為「標普 500 (系統)」 3. 自訂排行條件：請加入「美股殖利率排行榜」
a. 挑選名次「100」名 4. 選股腳本：請加入「殖利率相關資訊」 5. 按下「完成」即完成「標普500_殖利率排行榜」在玲瑯滿目的美股商品中，投資者銀彈有限的情況下，僅能針對幾檔美股商品進行投資時，不妨可以試試看製作排行榜來當作參考依據。

上述分享「標普500_殖利率排行榜」就是其中一種濾網來做篩選，提供給大家參考。
財報欄位
美股財報的選股欄位，在 *.12.01 版本，共新增 71 個美股的 XS 選股欄位，也有新增相關的內建選股條件，方便大家直接加入使用。

接下來，將用「股東權益報酬率」這個有支援美股的選股欄位，製作「美股普通股_巴菲特概念股」選股策略，如下圖，來為大家做介紹

該策略的選股條件是參考選股中心→系統→價值投資→927.巴菲特概念股：
年_連續 10 年股東權益報酬率大於 8 % 年_連續 3 年股東權益報酬率大於 15 % 日_收盤價大於近 60 日平均這次直接透過內建的選股條件來製作，不用到選股腳本進行撰寫，請參考下圖，依指示操作。

1. 在選股中心新增「美股普通股_巴菲特概念股」策略 2. 市場別：請設定為「美股」 3. 範圍：請設定為「美股普通股(全部)」 4. 選股條件：請加入以下三個條件
a. 年_連續 10 年股東權益報酬率都大於 8 % b. 年_連續 3 年股東權益報酬率都大於 15 % c. 日_收盤價大於近 60 日平均 5. 按下「完成」即完成「美股普通股_巴菲特概念股」選股策略巴菲特的選股心法，其中一個就是針對「股東權益報酬率」來當作濾網篩選出好標的，

而在選股條件，有些常見的條件，可以直接點選加入，無須再自行撰寫腳本，

上述的「美股普通股_巴菲特概念股」選股策略，就是一個使用範例，供大家參考。
籌碼欄位
美股的籌碼欄位，在 *.12.01 版本，新增 5 個美股的選股欄位，與 4 個美股的資料欄位。

美股市場的籌碼面資料中，比較常見的是「機構持股」與「機構持股比重」這種依美國證券交易委員會規定每季公告的「SEC Form 13F」數據。

在美股商品技術分析圖→技術分析設定→副圖設定→商品指標，就有內建指標「機構持股」可以點選使用，如下圖。

這個內建指標，我們也可以透過 XS 自訂撰寫，使用美股的XS資料欄位「機構持股」與「機構持股比重」透過 Plot1 繪圖 XS 語法，以及繪圖設定來製作「XS自訂指標_機構持股」如下圖。

「XS自訂指標_機構持股」的程式碼，雖然圖上有說明，不過仍提供以下程式碼，方便大家複製貼上，動手操作。

```xs
//因為繪圖設定→查價視窗→數值顯示→%，會自動 × 100，所以該數值要 / 100，才會在顯示正確 % 數值。
plot1(GetField("機構持股比重", "Q")/100,"比重",Checkbox:=1);
plot2(GetField("機構持股", "Q"),"數值",Checkbox:=0);
```

上述的XS自訂指標腳本的程式碼撰寫完成，並編譯成功後，請依照以下兩個步驟，進行繪圖設定，與加入副圖到技術分析圖中。

第一步：撰寫「XS自訂指標_機構持股」指標腳本，請參考下圖，依照指令進行。

1. 在撰寫程式碼腳本處，右鍵選單→繪圖設定。 2. 繪圖設定→繪圖樣式的「型式、樣式、顏色、座標軸」請依照圖示進行設定，並按下確定。 3. 繪圖設定→查價視窗的「數值顯示、小數格式」請依照圖示進行設定，並按下確定。第二步：將「XS自訂指標_機構持股」加入到技術分析圖中，如下圖

1. 回到XQ的美股商品技術分析圖→技術分析設定→副圖設定→搜尋「XS自訂指標_機構持股」加入到副指標中，並按下完成。透過上述步驟，即完成「XS自訂指標_機構持股」加入到美股商品分析技術圖中。

抽取幾個常見的美股基本面、財報、籌碼欄位，來為大家簡單介紹使用方式，以上皆為付費功能，需購買美股分析模組每月500元，方可完整體驗。

若大家使用後，有任何的心得與建議，也歡迎來信至 XQservice@XQ.com.tw 回饋分享，

感謝大家的收看，咱們下次見～

延伸閱讀：
XS美股欄位表 進一步了解美股分析模組 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="取得資料欄位時的「對位問題」"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="取得資料欄位時的「對位問題」"></a>
# [取得資料欄位時的「對位問題」](https://www.xq.com.tw/lesson/xspractice/%E5%8F%96%E5%BE%97%E8%B3%87%E6%96%99%E6%AC%84%E4%BD%8D%E6%99%82%E7%9A%84%E3%80%8C%E5%B0%8D%E4%BD%8D%E5%95%8F%E9%A1%8C%E3%80%8D/)

大家好，本文將為大家介紹「如何正確使用欄位資料」。
有鑑於您可能會經常遇到這類問題，所以我們將問題分為兩個部分：
變數的對位方式 資料欄位的對位方式

變數的對位方式
很多 XQ 的資深用戶，經常使用「變數(Variable)」這個概念。
尤其是當我們想要進行 重複性較高的運算式 時，通常都會將資料欄位包入變數。
我們用以下情境來說明舉例：

用戶想要計算近 4 期的月營收平均，於是寫出下方 XS 代碼。

```xs
// 計算 近 4 期的月營收平均
Value1 = GetField("月營收", "M");
Value2 = Average(Value1+Value1[1]+Value1[2]+Value1[3], 4);
```

但由於變數後方的中括號，其實是代表著不同頻率下的資料內容

於是，系統將這串 XS 代碼理解成：
Value1 = 取得當期K棒日期之變數內容 = GetField("月營收", "M")
Value1[1] = 取得前 1 期K棒日期之變數內容 = GetField("月營收", "M")

您沒有看錯，Value1 和 Value1[1] 都會取得相同的欄位資料！

由於最新一期的月營收尚未公布，而用戶又誤將變數後方的中括號，當作是資料欄位的期數。
這時，如果在月營收尚未公布之前，就開始計算 T 日和 T+1 的月營收，用戶就會取得相同的資料，進而導致運算結果在某些時間點可能有誤。

▼ 從下方 2023-06-30 的選股結果就可發現，由於台泥 5 月營收為 95.96 億元；4月營收為 91 億元。但時間到了2023-07-03 ，當期月營收和上一期月營收卻都變成 95.96 億元。這就是誤將變數中括號內的期數，誤作成資料欄位期數的結果。

因此，當我們在使用變數時，要特別留意中括號的意義，以免造成運算結果錯誤唷！

資料欄位的對位方式
那要如何正確地取得資料欄位呢？
其實方法非常簡單，您只需在呼叫資料時，於 GetField 後方加上中括號與期數。

因為您只需要在資料取得時，直接在後方加上中括號。
系統就會根據您輸入的 頻率 與 期數，自動判斷您想要取得的欄位資料究竟是第幾期？

GetField("月營收", "Ｍ")[1] = 上一期月頻率的月營收
GetField("集保張數", "W")[3] = 前三期周頻率的集保張數
( 以此類推 )

不知道這篇對資料欄位的對位問題的說明，是否有解答您過去遇到的困難呢？
XS 團隊會繼續優化相關語法功能與應用，陪伴投資人一同茁壯成長，成為您專研量化交易的最佳夥伴。

我們下次見！ |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="選股中心創掛牌新高與大單欄位的應用"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="選股中心創掛牌新高與大單欄位的應用"></a>
# [選股中心創掛牌新高與大單欄位的應用](https://www.xq.com.tw/lesson/xspractice/%E9%81%B8%E8%82%A1%E4%B8%AD%E5%BF%83%E5%89%B5%E6%8E%9B%E7%89%8C%E6%96%B0%E9%AB%98%E8%88%87%E5%A4%A7%E5%96%AE%E6%AC%84%E4%BD%8D%E7%9A%84%E6%87%89%E7%94%A8/)

創公司掛牌新高的選股條件與語法應用 大單相關的選股條件與語法應用

創公司掛牌新高的選股條件與語法應用
先前常常收到客戶反應，想要篩選出「月營收創歷史新高」這樣子的公司，可是礙於不知道這家公司歷史以來到底有多少期資料，所以無法辦到。

針對這個需求，在這次的版本中，我們針對「月營收」與「每股稅後淨利(元)」的這兩個欄位時，提供了「創公司掛牌後新高」的內建條件。請參考下圖。

「創公司掛牌後新高」的判斷方式，是透過這次改版新增的 GetFieldStartOffset 語法。這個語法可以讓你抓到這個欄位第一筆的位置，知道這個欄位總共有多少筆之後，腳本就可以很容易的判斷目前最新一期數值到底是不是新高了。

如果除了「月營收」跟「每股稅後淨利(元)」之外，你想要運用在其他欄位的話，可以參考以下的寫法：

```xs
value1 = GetFieldStartOffset("月營收", "M");
if value1 = 0 then begin
 ret = 1; // 只有1期, 就當成創新高了吧
 outputField1(GetField("月營收", "M"),"月營收");
end else if value1 > 0 then begin
 // 算出前N期的最大值
 value2 = Highest(GetField("月營收", "M")[1], value1);
 if GetField("月營收", "M") > value2 then ret = 1;
 outputField1(GetField("月營收", "M"),"月營收");
end;
```

> [!IMPORTANT]
> 請GetFieldStartOffset目前僅支援選股腳本。另外 GetFieldStartOffset 所定義的第一筆資料的位置，是依照公司上市櫃日期所決定的，不包含興櫃期間的資料。

大單相關的選股條件與語法應用
在7.02/3.02版時，我們推出了大單相關的欄位(包含買進大單量，賣出大單量等)，當時這些欄位僅能在策略雷達/自動交易端使用。在這個版本內，XS選股也同步支援這些欄位。

在XS選股中心內，新增條件時可以直接找到這些欄位以及相關條件直接使用。

當然，你也可以從選股腳本內直接使用這些欄位。以下是一個腳本範例，用來篩選大戶買超的股票，大戶的定義是（買進特大單＋買進大單）－（賣出特大單＋賣出大單）＞ 0的股票。

```xs
value801 = GetField("買進特大單量", "D") + GetField("買進大單量", "D");
value802 = GetField("賣出特大單量", "D") + GetField("賣出大單量", "D");
value888 = value801 - value802;
//大單買超 = (買進特大單+買進大單)-(賣出特大單+賣出大單) > 0
if value888 > 0 then ret = 1;
outputField1(value888,"大單買超由大到小排名",order:=1);

```

以下這張表格，是此次新增的選股欄位與說明表格，詳細的介紹可點選連結查閱，有了這些選股欄位，投資人就能在盤前先做功課，運用選股中心篩選出來的結果，觀察近期大戶資金布局在何處，搶得先機。
以下這張表格，是此次新增的選股欄位與說明表格：

| 選股欄位名稱 | 說明 |
| :--- | :--- |
內盤成交次數/外盤成交次數 | 當日的成交筆數/以內盤成交的成交筆數/以外盤成交的成交筆數
買進特大單量/買進大單量/買進中單量/買進小單量 | 依照單筆交易金額分級，統計當日以外盤成交的特大單/大單/中單/小單的成交量(張數)
賣出特大單量/賣出大單量/賣出中單量/賣出小單量 | 依照單筆交易金額分級，統計當日以內盤成交的特大單/大單/中單/小單的成交量(張數)
買進特大單金額/買進大單金額/買進中單金額/買進小單金額 | 依照單筆交易金額分級，統計當日以外盤成交的特大單/大單/中單/小單的成交金額(元)
賣出特大單金額/賣出大單金額/賣出中單金額/賣出小單金額 | 依照單筆交易金額分級，統計當日以內盤成交的特大單/大單/中單/小單的成交金額(元)
買進特大單成交次數/買進大單成交次數/買進中單成交次數/買進小單成交次數 | 依照單筆交易金額分級，統計當日以外盤成交的特大單/大單/中單/小單的成交筆數
賣出特大單成交次數/賣出大單成交次數/賣出中單成交次數/賣出小單成交次數 | 依照單筆交易金額分級，統計當日以內盤成交的特大單/大單/中單/小單的成交筆數

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="台股逐筆撮合的連續成交Tick序列"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="台股逐筆撮合的連續成交Tick序列"></a>
# [台股逐筆撮合的連續成交Tick序列](https://www.xq.com.tw/lesson/xspractice/%E5%8F%B0%E8%82%A1%E9%80%90%E7%AD%86%E6%92%AE%E5%90%88%E7%9A%84%E7%A9%BF%E5%83%B9tick/)

大家先看一下底下這張圖：

注意到在12:51:04秒左右有三筆資料後面有標示星號，成交價分別是11.70元26張，11.75元51張，以及11.80元23張，可是這個星號是什麼意思呢？

之所以會有這個星號，是因為這三筆成交是由同一筆買進委託所產生的，而這一筆買進委託的委託量是100張，而且委託價格是11.8元。當交易所收到這一筆委託時，因為當時最低的委賣價是11.70元，只有26張委賣量，所以這一筆委託先撮合出11.70元26張的成交之後，接著交易所繼續撮合次高的委賣價11.75元51張，最後再撮合11.80元的委賣23張(26 + 51 + 23 = 100張)。

底下是這筆成交發生之前委賣五檔委託簿的樣子，請注意委賣價格以及委賣數量。

當發生這種情形時，因為產生了三筆不同的成交，所以交易所會傳送出3筆連續的成交資料。可是如果我們去思考這次成交的買賣力道時，觀察的重點應該是產生這一系列成交的這一筆委買單，所以在大單追蹤這個畫面內我們用星號把這幾筆成交資料的關係標示出來，計算單別時也會把這三筆成交加總，來判斷這一筆的實際力道。

那麼這樣子的資訊，是不是可以從XS內得知呢？Yes，We Can。

```xs
value1 = GetField("TickGroup", "Tick");
```

在Tick資料內有一個欄位叫做「TickGroup」，這個欄位是用來標示這一筆成交資料是怎麼撮合出來的，總共有5種可能的數值：
-1: 表示這一筆成交並不是逐筆撮合所產生的。例如開盤的第一筆成交，收盤的那一筆成交都會標示-1， 0: 表示這一筆成交是逐筆撮合所產生的，只有撮出一筆， 1: 表示這一筆成交是連續成交序列內的第一筆， 2: 表示這一筆成交是連續成交序列內中間的任何一筆， 3: 表示這一筆成交是連續成交序列內的最後一筆

如果對照到上面大單分析的那張圖的話，7.70元的那一筆的TickGroup是1，7.71 元以及7.72元的那兩筆的TickGroup都是2, 7.74元的那一筆的TickGroup是3。在腳本內，就可以藉由TickGroup這個欄位，把連續成交序列的每一筆Tick找出來，然後計算這個連續成交序列的總成交量。

為了簡化讀取Tick資料的流程，7.02版新增了一個ReadTicks函數，可以協助我們自動把把連續成交序列合併統計，讓腳本的的邏輯可以更簡單。我們把上一篇文章「7.02-盤中即時資料欄位的應用」內的篩選大單的腳本改用ReadTicks這個函數來重寫：

```xs
input: filterMode(1, "篩選方式", inputkind:=dict(["買盤",1], ["賣盤",-1]));
input: filterVolume(100, "大單門檻");

var: intrabarpersist readtick_cookie(0);// ReadTicks內部使用, 每次呼叫時請照實傳入
array: tick_array[100, 11](0);// 需要宣告一個2維陣列來儲存Tick資料
var: row_count(0), idx(0);

// 讀取Tick資料
row_count = ReadTicks(tick_array, readtick_cookie);
for idx = 1 to row_count begin
  if tick_array[idx, 5] = filterMode and tick_array[idx, 10] >= filterVolume then begin
    ret=1;
  end;
end;

```

要呼叫ReadTicks之前，我們要先準備一個二維的陣列(第5行的tick_array)，一個二維陣列就像是一個Excel的表格，呼叫完ReadTicks之後，每一橫列會儲存一筆Tick資料，而這一筆Tick資料的每個欄位則會存在這個橫列的每一行裡面。

除了這個陣列之外，我們還需要準備一個變數(第4行的readtick_cookie)，這個變數用來讓ReadTicks函數可以紀錄上次讀到的Tick資料位置。

接著每一次洗價時，我們就請ReadTicks函數讀取上次洗價到目前為止的所有Tick資料(第13行)，ReadTicks會回傳這次讀回的筆數(row_count)，接著我們就用for迴圈來檢視讀到的Tick資料。

ReadTicks讀回的Tick資料會存放在tick_array內，最新的一筆是第一個橫列(row), 前一筆是第二個row，以下類推。每一個row內包含11個欄位(column)，內容分別是：
每一個 row 內包含 11 個欄位 (column)，內容分別是：
- 第 1 個 column 是日期
- 第 2 個 column 是時間
- 第 3 個 column 是成交價
- 第 4 個 column 是成交量
- 第 5 個 column 是內外盤註記
- 第 6 個 column 是這一筆Tick的編號(SeqNo)
- 第 7 個 column 存放的是這個連續成交序列總共有多少筆 (如果是 ReadTicks 遇到多筆連續成交序列時)
- 第 8 個 column 存放的是這個連續成交序列的第一筆的位置
- 第 9 個 column 存放的則是這個連續成交序列最後一筆的位置
- 第 10 個 column 存放的是這些連續成交序列的總成交量
- 第 11 個 column 存放的是這些連續成交序列的總成交金額(元)

```xs
if tick_array[idx, 5] = filterMode and tick_array[idx, 10] >= filterVolume then ...
```

上面這個判斷式內，tick_array[idx, 5]是內外盤註記，而tick_array[idx, 10]則是這一筆成交的總量(或是連續成交序列的總量)！

到這裡為止，我們已經跟大家說明了Tick資料的所有欄位以及應用的方式，大家如果有任何建議或是問題的話，麻煩到我們的臉書或是XS技術支援區留言。下次再見囉。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="Tick欄位的應用"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="Tick欄位的應用"></a>
# [Tick欄位的應用](https://www.xq.com.tw/lesson/xspractice/tick%E6%AC%84%E4%BD%8D%E7%9A%84%E6%87%89%E7%94%A8/)

什麼是Tick資料呢？Tick資料簡單的來說，就是每一筆成交資料。

從7.02版開始，你可以從腳本內取得每一筆成交資料的詳細資料，除了成交時間，成交價格，成交單量之外，還可以取得這一筆成交資料的內外盤標記(這一筆是外盤成交，還是內盤成交呢？)。以下是Tick資料所支援的所有欄位，以及從腳本內讀取資料的範例：

```xs
value1 = GetField("Date", "Tick");   // 成交日期，例如20200611 (2020年6月11日)
value2 = GetField("Time", "Tick");   // 成交時間，例如103011 (10點30分11秒)
value3 = GetField("Close", "Tick");  // 成交價格
value4 = GetField("Volume", "Tick"); // 成交單量
value5 = GetField("BidAskFlag", "Tick"); // 內外盤標記: 1代表外盤成交(紅色), -1代表內盤成交(綠色), 0代表中立
value6 = GetField("BidPrice", "Tick"); // 買進價格
value7 = GetField("AskPrice", "Tick"); // 賣出價格
value8 = GetField("SeqNo", "Tick");  // 資料編號, 每個交易日從1開始編制, 第一筆是1, 第二筆是2, 以下類推

value22 = GetField("Close", "Tick")[1]; // 前一筆成交價格
value23 = GetField("Close", "Tick")[2]; // 前兩筆成交價格
```

透過Tick資料，我們從腳本內就可以更掌握目前的行情資料。我們來看一個大單篩選的警示範例，假如我們希望商品出現單筆成交量大於100張的成交時就通知我們的話，可以用底下這個警示腳本來完成：

```xs
input: filterMode(1, "篩選方式", inputkind:=dict(["買盤",1], ["賣盤",-1]));
input: filterVolume(100, "大單門檻");

value1 = GetField("Time", "Tick"); // 時間
value2 = GetField("Close", "Tick"); // 價格
value3 = GetField("Volume", "Tick"); // 單量
value4 = GetField("BidAskFlag", "Tick"); // 外盤=1, 內盤=-1

if value4 = filterMode and value3 >= filterVolume then ret=1;
```

我們把這個腳本加到策略雷達內，選擇1分鐘頻率，勾選逐筆洗價，同時指定要篩選的參數，例如filterMode選擇買盤，filterVolume選擇100，這樣子就完成了。

> [!TIP]
> 上面這個腳本有使用到inputKind這個語法，讓參數設定時可以用選單的方式來挑選。有興趣的同學可以參考inputKind的說明。

Tick欄位 v.s. 報價欄位
在XS腳本內，你也可以透過報價欄位來取得盤中的行情欄位，例如q_Last可以取得最新一筆成交價格，q_TickVolume可以取得最新一筆成交單量，也可以透過q_BidAskFlag來取得最新一筆成交的內盤外盤註記。

那在這個腳本內為何不使用報價欄位，而要使用Tick欄位呢？

要說明這個問題時請大家先看底下這張圖：

從上面這張圖內我們可以觀察到XS洗價的執行方式：
啟動逐筆洗價時，當系統收到成交資料時就會觸發K棒的洗價，

- 一般而言每收到一筆成交就會觸發一次K棒的洗價，請注意上圖內的洗價時間是示意資料，實際洗價時間會因為電腦CPU，指定的洗價速度等因素而有差異，

- 可是如果快市的話，就有可能不是每一筆Tick都觸發一次洗價，而是好幾筆Tick才觸發一次洗價，例如在上面這張圖內, Tick編號#3跟Tick編號#4兩筆資料的間隔時間很短，所以觸發洗價時已經收到兩筆Tick了(編號#3跟編號#4)，

- 報價欄位所回傳的是目前最新的行情，在上面這個情形時就是編號#4的數值，所以大部分的時候報價欄位的數值會等於最新一筆Tick的資料，

- 可是有可能在電腦洗價時又收到了更多的成交資料，此時報價欄位會更新，q_Last就不一定會跟洗價時K棒的Close是一樣的數值，例如在上面這張圖內，執行第四次洗價時如收到了編號#6的Tick的話, q_Last會被更新成編號#6的資料，此時就會跟第四次洗價時所抓到的價格不一樣，

- 如果在洗價時去抓取Tick資料的話，則系統會保證此時抓到的Tick資料跟洗價當時的K棒內容是一致的。例如第四次洗價時抓到的Tick會是編號#5的Tick，縱使洗價當時編號#6的Tick已經收到了，

- 腳本可以在洗價當時透過讀取Tick資料的方式來抓到兩次洗價之間的所有Ticks：例如在第四次洗價時抓到的Tick是編號#5的Tick，此時腳本可以再抓取「前一筆」Tick，就會抓到編號#4的Tick，K棒洗價的速度可以透過系統的設定功能來指定，如果調整成最快的話約是125ms會執行一次洗價，如果調整成最慢的話則是每1秒執行一次洗價，使用者可以依照腳本複雜度，CPU的等級等來決定合適的數值。

如何抓到每一筆Tick資料
雖然在快市時不一定可以每次收到成交資料時就執行洗價，可是我們還是可以利用以下的技巧來抓到上一次洗價到這一次洗價之間的所有Tick資料：
每一筆Tick資料有一個「SeqNo」的欄位，這個欄位代表的是這一筆Tick是這個交易日的第幾筆，每個交易日會重新從1開始編制， Tick資料跟1分K, 5分K一樣，都是一個序列，可以使用[1]，[2]的語法來取得前一筆的數值所以如果我們在腳本內記住這一次洗價時的SeqNo的數值，然後在下一次洗價時比對當時的SeqNo，就可以知道這兩次洗價之間相隔幾筆Tick，然後透過[n]的方式就可以把這些Tick資料都讀出來。例如上一次洗價時SeqNo是3，而這一次洗價時SeqNo是5，那麼這兩次之間就隔了兩筆Tick，所以腳本只要讀取當時的Tick，跟前一筆Tick資料(共兩筆)，就可以清楚知道兩次洗價之間市場的變化了。

我們把大單篩選的腳本改用這樣子的方式來撰寫。觸發的邏輯改成是如果兩次洗價之間出現了任何一筆大單的話，都顯示警示。

```xs
input: filterMode(1, "篩選方式", inputkind:=dict(["買盤",1], ["賣盤",-1]));
input: filterVolume(100, "大單門檻");

var: intrabarpersist last_seqno(0);// 上次洗價時最後一筆Tick的SeqNo
var: curr_seqno(0);// 這次洗價時最後一筆Tick的SeqNo

if Date <> CurrentDate then return;// 只跑今日的資料

curr_seqno = GetField("SeqNo", "Tick");// 最新一筆Tick編號

if last_seqno = 0 then last_seqno = curr_seqno - 1; // 第一次洗價時只洗當時那一筆

var: seq_no(0), offset(0);
seq_no = curr_seqno;
offset = 0;
while seq_no > last_seqno begin

  // 讀取Tick資料
  value1 = GetField("Time", "Tick")[offset];
  value2 = GetField("Close", "Tick")[offset];
  value3 = GetField("Volume", "Tick")[offset];
  value4 = GetField("BidAskFlag", "Tick")[offset];

  if value4 = filterMode and value3 >= filterVolume then begin
    ret=1;
  end;

  seq_no = seq_no - 1;
  offset = offset + 1;
end;

last_seqno = curr_seqno;

```

上面這個腳本比較長一點，所以我們花點篇幅說明處理的方式:
第3行內宣告了last_seqno這個變數，這個變數是用來儲存上一次洗價時所對應的SeqNo，由於我們採用逐筆洗價方式，所以必須宣告成intrabarpersist，才可以讓這個變數的數值在同一根bar內可以正常更新， 每一次洗價時腳本會抓取當時的SeqNo (第9行)，然後把這個數值存在curr_seqno這個變數內， 腳本內使用while語法，來跑過curr_seqno到last_seqno之間的所有資料(第16行開始)，
假如last_seqno是3，而curr_seqno是5的話，那這兩次洗價之間總共有2筆Tick資料，編號分別是4跟5(3的那一筆上一次已經讀過了)， 腳本內宣告了offset這個變數(第13行)，用來控制往前讀取的位置，一開始offset = 0(第15行)， 進入while loop後，第一次讀到的是GetField("Close", "Tick")[0](第19~22行)，也就是編號5的這一筆， 每跑一次loop，offset會加1(第29行)，表示下一次我們會往前再讀一筆， 第二次while loop讀到的是GetField("Close", "Tick")[1], 也就是編號4的這一筆， 我們用seq_no這個變數來控制while loop的次數，seq_no一開始是curr_seqno(第14行)， 也就是5， 每一次loop seq_no會減1(第28行)（因為我們是往前讀）， 這個while loop總共跑了2次：第一次seq_no = 5，第二次=4，第二次跑完之後seq_no變成3， 此時就跳出loop了， 全部跑完之後，要記得更新last_seqno (第32行)這次的介紹到這裡先告個段落，在下一篇文章「台股逐筆撮合的連續成交Tick序列」，我們會再介紹台股逐筆撮合制度所產生的另外一種資料，也會跟大家介紹系統內建的ReadTicks函數，透過這個函數來簡化讀取Tick資料的流程。

下次再見囉。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="盤中即時資料欄位的應用"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="盤中即時資料欄位的應用"></a>
# [盤中即時資料欄位的應用](https://www.xq.com.tw/lesson/xspractice/%E7%9B%A4%E4%B8%AD%E5%8D%B3%E6%99%82%E8%B3%87%E6%96%99%E6%AC%84%E4%BD%8D%E7%9A%84%E6%87%89%E7%94%A8/)

什麼叫做「盤中即時資料欄位」呢？在7.02/3.02之前的版本，資料欄位(例如內盤量，外盤量，外資買賣超，等)所支援的最快更新頻率都是日頻率，也就是說如果你的自訂指標或是策略雷達是執行分鐘頻率的話，你還是只能抓到昨日的數值，沒辦法抓到更精細的欄位，例如9:01分的數值，9:02的數值等。

以外資買賣超這種一天才公布一次的欄位而言，受限於資料來源，XS沒辦法提供分鐘頻率的數值。可是如內盤量，外盤量這種是以成交明細所統計出來的資料欄位，從7.02/3.02版開始，你就可以抓到每分鐘的數值了。因為這些資料欄位的數值可以在盤中即時抓到，即時更新，所以我們稱這些欄位為「盤中即時資料欄位」，換句話說，所謂「盤中即時資料欄位」指的是支援分鐘頻率的資料欄位。

從腳本內要怎麼樣取得這些欄位呢？一樣是透過GetField的語法：

```xs
value1 = GetField("內盤量", "1"); // 取得1分鐘頻率的數值
value2 = GetField("內盤量", "2"); // 取得2分鐘頻率的數值
value3 = GetField("內盤量", "5"); // 取得5分鐘頻率的數值
value4 = GetField("內盤量");      // 不指定頻率: 依照目前執行的頻率
```

語法上最大的差異是頻率欄位可以直接傳入預期的分鐘頻率(只要是XQ目前有支援的分鐘頻率都可以使用)。如果雷達/指標是執行分鐘頻率的話也可以不指定頻率，執行時會依照執行的頻率來抓取對應的數值。

透過IDE的插入欄位功能也可以選到常用的分鐘頻率：

這些「盤中即時資料欄位」所支援的最小頻率都是1分鐘。如果你使用的是比較大的頻率的話(例如5分鐘頻率)，那腳本所抓到的數值就會依照這個欄位的定義來決定。計算的方式有兩種：
累積值：例如「內盤量」欄位，5分鐘頻率所抓到的「內盤量」就是這5分鐘內每一個1分鐘「內盤量」的累積數值， 期末值：例如「均價」欄位，5分鐘頻率所抓到的「均價」就是最後1分鐘的「均價」數值底下列出7.02版所支援的「盤中即時資料欄位」，如果想要了解欄位的詳細內容的話，請點選每個欄位名稱的連結。
底下列出 7.02 版所支援的「盤中即時資料欄位」，如果想要了解欄位的詳細內容的話，請點選每個欄位名稱的連結。

| 欄位名稱 | 說明 |
| :--- | :--- |
成交金額 | 當分鐘的成交金額
均價 | 開盤到目前為止的成交均價
內盤量/外盤量 | 當分鐘的內盤成交量/外盤成交量
上漲量/下跌量 | 當分鐘的上漲成交量/下跌成交量
估計量 | 到目前為止的當日估計量
量比 | 估計量 / 昨日整日的成交量
總成交次數/內盤成交次數/外盤成交次數 | 當分鐘的成交筆數/以內盤成交的成交筆數/以外盤成交的成交筆數
買進特大單量/買進大單量/買進中單量/買進小單量 | 依照單筆交易金額分級，統計當分鐘以外盤成交的特大單/大單/中單/小單的成交量(張數)
賣出特大單量/賣出大單量/賣出中單量/賣出小單量 | 依照單筆交易金額分級，統計當分鐘以內盤成交的特大單/大單/中單/小單的成交量(張數)
買進特大單金額/買進大單金額/買進中單金額/買進小單金額 | 依照單筆交易金額分級，統計當分鐘以外盤成交的特大單/大單/中單/小單的成交金額(元)
賣出特大單金額/賣出大單金額/賣出中單金額/賣出小單金額 | 依照單筆交易金額分級，統計當分鐘以內盤成交的特大單/大單/中單/小單的成交金額(元)
買進特大單成交次數/買進大單成交次數/買進中單成交次數/買進小單成交次數 | 依照單筆交易金額分級，統計當分鐘以外盤成交的特大單/大單/中單/小單的成交筆數
賣出特大單成交次數/賣出大單成交次數/賣出中單成交次數/賣出小單成交次數 | 依照單筆交易金額分級，統計當分鐘以內盤成交的特大單/大單/中單/小單的成交筆數
累計委買/累計委賣/累委買筆/累委賣筆 | 由交易所公布，開盤到目前的累計委買委賣張數(口數)，以及累計委買委賣筆數
累買成筆/累賣成筆 | 由交易所公布，開盤到目前的以委買成交的筆數以及以委賣成交的筆數，支援期貨／選擇權
累計成交/累成交筆 | 由交易所公布，開盤到目前的累計成交數量以及成交筆數
漲停家數/跌停家數/上漲家數/下跌家數 | 目前最新成交價是漲停/跌停/上漲/下跌的家數統計
內盤家數/外盤家數 | 當分鐘的內盤家數/外盤家數
基差 | 當分鐘現貨價格與期貨價格之間的差額
Delta/Gamma/Theta/Vega/RHO | 權證/選擇權的希臘數字
理論價 | 目前最新的理論價格
波動率 | 當分鐘的波動率
隱含波動率 | 當分鐘的隱含波動率
買權成交量/賣權成交量 | 開盤到現在的買權/賣權成交量總和。夜盤商品不納入計算。

看完欄位清單之後，那我們就使用這些欄位來做幾個簡單的應用。

首先開啟IDE，新增一個指標腳本，取名為「當日賺賠」，輸入以下的程式碼：

```xs
value1 = GetField("均價");
value2 = weightedclose - value1;

plot1(value2, "賺賠");
```

開啟技術分析圖，切換到5分鐘頻率，然後加入這個指標，顯示方式改成正負柱狀圖，你會看到類似下方的畫面。

在這個指標內所繪製的是當根K棒的加權平均價(WeightedClose)減去當日均價的數值。這個數值如果是正的，表示到目前為止，今天進場的人大多是賺錢的。你也可以試看看切換不同的分鐘頻率。

「即時資料欄位」除了可以在自訂指標內使用之外，當然也可以在雷達內使用。接下來新增一個警示腳本，取名為「特大單買盤進場」，然後輸入以下的程式碼：

```xs
value1 = GetField("買進特大單量", "1") - GetField("賣出特大單量", "1");
if value1 > 0 then ret=1;
```

把這個腳本加到策略雷達內，選擇1分鐘頻率，勾選逐筆洗價，K棒內單次觸發，指定你想要監控的商品，開始執行後如果當分鐘特大單是買超的話，策略雷達就會產生警示，提醒你這檔商品可能有大戶要開始進場了。

到這裡對於「即時資料欄位」這個新的功能是否有更清楚了呢？小編在這裡做個整理：「即時資料欄位」是盤中會即時更新的資料序列，主要的應用情境是分鐘頻率，支援自訂指標以及策略雷達（並不支援選股）。至於回測的功能，我們還在努力開發中，等可以使用時會盡快讓大家知道。

下一篇文章「Tick欄位的應用」我們將會為你介紹另外一個功能，有興趣的同學可以繼續看下去！ |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="利用InputKind函數製作跨頻率週期的選取介面"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="利用InputKind函數製作跨頻率週期的選取介面"></a>
# [利用InputKind函數製作跨頻率週期的選取介面](https://www.xq.com.tw/lesson/xspractice/%E5%88%A9%E7%94%A8inputkind%E5%87%BD%E6%95%B8%E8%A3%BD%E4%BD%9C%E8%B7%A8%E9%A0%BB%E7%8E%87%E9%80%B1%E6%9C%9F%E7%9A%84%E9%81%B8%E5%8F%96%E4%BB%8B%E9%9D%A2/)

使用XS撰寫完成策略後，在介面上輸入參數可能會輸入錯誤的數值，雖然執行後會秀出錯誤訊息告知，但時常會造成困擾，所以我們提供InputKind函數讓使用者可以在腳本內控制介面的行為，讓大家能在介面選擇輸入值，就能避免輸入錯誤的狀況發生。

我們以KD指標的跨頻率語法為例。打開xfMin_stochastic函數我們可以看到，在計算有支援跨分鐘頻率的KD指標時我們會需要以下資訊：
頻率 資料期數 K值平滑期數 D值平滑期數 輸出RSV值 輸出K值 輸出D值其中，指定的分鐘頻率僅支援1分鐘、5分鐘、10分鐘、15分鐘、30分鐘與60分鐘，以上6種分鐘頻率。在6.42版以前，XS沒有提供InputKind函數的狀況下，我們會使用input函數指定分鐘頻率，但是在介面可能會輸入到沒有支援的指定頻率，執行後發現此錯誤往往讓使用者覺得困擾。

在有了InputKind函數之後，我們就能將可支援的分鐘頻率，製作成選項顯示在介面上，讓使用者在這些選項中選擇，就能避免輸入錯誤的狀況發生。

以上是InputKind的應用範例，InputKind使用說明可以參考XSHelp說明。改寫後的跨分鐘與日頻率KD指標腳本如下：

```xs
 // 跨頻率KD指標，預設跨頻率為30分
 // 不支援大頻率跨小頻率，例如：
 // 不支援主頻率60分鐘，跨頻率計算30分鐘KD技術指標。
 //
 input:    Length(9, "天數"), RSVt(3, "RSVt權數"), Kt(3, "Kt權數"),
         FreqType("30", "跨頻率週期", inputkind:=dict(["1分鐘","1"],["5分鐘","5"],["10分鐘","10"],["15分鐘","15"],["30分鐘","30"],["60分鐘","60"],["日","D"],["還原日","AD"]));
 variable: rsv(0), k(0), _d(0);

 if barfreq <> "Tick" and barfreq <> "Min" and barfreq <> "D" and barfreq <> "AD" then raiseruntimeerror("此範例僅支援分鐘、日與還原日頻率");

 xfMin_stochastic(FreqType, Length, RSVt, Kt, rsv, k, _d);

 Plot1(k, "分鐘與日K(%)");
 Plot2(_d, "分鐘與日D(%)");

 // 防呆，大頻率跨小頻率時，在線圖秀不支援
 //
 switch (FreqType)
 begin
     case  "1":
         if barfreq <> "Tick" and barfreq <> "Min" then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於1分鐘");
         if barinterval <> 1 then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於1分鐘");
         setplotlabel(1, "1分K(%)");
         setplotlabel(2, "1分D(%)");

     case  "5":
         if barfreq <> "Tick" and barfreq <> "Min" then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於5分鐘");
         if barinterval <> 1 and barinterval <> 2 and barinterval <> 3 and barinterval <> 5 then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於5分鐘");
         setplotlabel(1, "5分K(%)");
         setplotlabel(2, "5分D(%)");

     case "10":
         if barfreq <> "Tick" and barfreq <> "Min" then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於10分鐘");
         if barinterval <> 1 and barinterval <> 2 and barinterval <> 3 and barinterval <> 5 and barinterval <> 10 then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於10分鐘");
         setplotlabel(1, "10分K(%)");
         setplotlabel(2, "10分D(%)");

     case "15":
         if barfreq <> "Tick" and barfreq <> "Min" then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於15分鐘");
         if barinterval <> 1 and barinterval <> 2 and barinterval <> 3 and barinterval <> 5 and barinterval <> 10 and barinterval <> 15 then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於15分鐘");
         setplotlabel(1, "15分K(%)");
         setplotlabel(2, "15分D(%)");

     case "30":
         if barfreq <> "Tick" and barfreq <> "Min" then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於30分鐘");
         if barinterval <> 1 and barinterval <> 2 and barinterval <> 3 and barinterval <> 5 and barinterval <> 10 and barinterval <> 15 and barinterval <> 20 and barinterval <> 30 then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於30分鐘");
         setplotlabel(1, "30分K(%)");
         setplotlabel(2, "30分D(%)");

     case "60":
         if barfreq <> "Tick" and barfreq <> "Min" then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於60分鐘");
         if barinterval <> 1 and barinterval <> 2 and barinterval <> 3 and barinterval <> 5 and barinterval <> 10 and barinterval <> 15 and barinterval <> 20 and barinterval <> 30 and barinterval <> 45 and barinterval <> 60 then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於60分鐘");
         setplotlabel(1, "60分K(%)");
         setplotlabel(2, "60分D(%)");

     case "D":
         if barfreq <> "Tick" and barfreq <> "Min" and barfreq <> "D" and barfreq <> "AD" then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於日");
         setplotlabel(1, "日K(%)");
         setplotlabel(2, "日D(%)");

     case "AD":
         if barfreq <> "Tick" and barfreq <> "Min" and barfreq <> "D" and barfreq <> "AD" then raiseruntimeerror("不支援大頻率跨小頻率：主頻率大於還原日");
         setplotlabel(1, "還原日K(%)");
         setplotlabel(2, "還原日D(%)");

 end; |
```

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="利用GetSymbolInfo函數計算選擇權的希臘字母Delta"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="利用GetSymbolInfo函數計算選擇權的希臘字母Delta"></a>
# [利用GetSymbolInfo函數計算選擇權的希臘字母Delta](https://www.xq.com.tw/lesson/xspractice/%E5%88%A9%E7%94%A8getsymbolinfo%E5%87%BD%E6%95%B8%E8%A8%88%E7%AE%97%E9%81%B8%E6%93%87%E6%AC%8A%E7%9A%84%E5%B8%8C%E8%87%98%E5%AD%97%E6%AF%8Ddelta/)

我們以選擇權的Delta為例。打開BSDelta函數我們可以看到，在計算選擇權希臘字母時我們會需要以下資訊：
買賣權 標的價格 履約價 到期天數 無風險利率 持有成本 波動率其中，買賣權、履約價、到期天數算是商品的基本資料，是固定的。在6.42版以前, XS沒有提供GetSymbolInfo函數的狀況下，我們會需要一點巧門來取得這些資訊。

```xs
買賣權=midstr(symbol,6,1); //利用商品名稱取得
履約價=strtonum(midstr(symbol,7,strlen(symbol) - 9)); //利用商品名稱取得
到期天數利用daystoexpirationtf函數，直接寫死找出每月第三個星期三
```

也就是因為這些規則是死的，利用商品名稱取得資訊的方式讓我們之前系統內建指標只能提供台指選專用的指標。更不要說碰到颱風或農曆春節導致結算日異動時，這個指標就會失真。

在有了GetSymbolInfo函數之後，這些資訊我們就可以直接使用

```xs
買賣權=GetSymbolInfo("買賣權");
履約價=GetSymbolInfo("履約價");
到期天數=DateDiff(GetSymbolInfo("到期日"), Date) + 1;
```

所有的選擇權商品，XS都會提供這些商品基本資料，我們就可以寫出通用版的Delta指標，而不是僅限台指選專用。改寫後的腳本如下：

```xs
input:
     iRate100(2,"無風險利率%"),
     iHV(20,"標的歷史波動率計算期間");

 variable:vStrikePrice(0),vVolity100(0),vTTMdays(0);

 if symboltype <> 5 then
     raiseruntimeerror("僅支援選擇權");

 if iHV > 0 then
     vVolity100 = HVolatility(getsymbolfield("Underlying","收盤價","D"),iHV)
 else
     vVolity100 = 20;

 vStrikePrice = getsymbolinfo("履約價");
 vTTMdays = DateDiff(GetSymbolInfo("到期日"), Date) + 1;

 value1 = bsdelta(leftstr(getsymbolinfo("買賣權"),1), //C表買權、P表賣權
 getsymbolfield("Underlying","收盤價"), //標的價格
 vStrikePrice, //履約價
 vTTMdays, //到期天數
 iRate100, //無風險利率
 0, //持有成本
 vVolity100); //波動率

 plot1(value1,"Delta");
```

計算Delta需要的所有參數，都是透過GetSymbolInfo取得系統資料，所以現在不止台指選可以用，其他電子選、金融選甚至是個股選擇權都可以使用這個Delta指標。 有沒有注意到在取得標的價格的地方我們是怎麼寫的？

```xs
getsymbolfield("Underlying","收盤價");
```

是的，這次我們同時強化了GetSymbolField的功能，在商品欄的位置可以直接使用"Underlying"或"標的"動態取得標的商品。如此一來，不止是本篇文章說明的選擇權希臘字母可以計算，要自行計算期貨商品的價差值也是可行的。

以上是GetSymbolInfo的應用範例，GetSymbolInfo完整的支援項目清單請參考XSHelp說明 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="關於背離的寫法"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="關於背離的寫法"></a>
# [關於背離的寫法](https://www.xq.com.tw/lesson/xspractice/%E9%97%9C%E6%96%BC%E8%83%8C%E9%9B%A2%E7%9A%84%E5%AF%AB%E6%B3%95/)

首先，XS內建函數中有一個叫作線性回歸斜率(linearregslope)，如果您在腳本編輯器中按F1，會跳到函數的說明頁，您輸入linearregslope按enter，會看到以下的說明

什麼是線性迴歸呢?

就像上圖一樣，我們找出一條線， 這條線距離所有要計算的價位最近，就是所謂的線性迴歸，透過線性迴歸，我們可以找出價位群體的方向，這條線的斜率就是linearregslope。

當數據呈現上昇分佈時，線性迴歸的斜率是正的，下降分佈時，線性迴歸的斜率是負的。

小編運用這個原理，寫出以下的背離函數：

```xs
input:price(numericsimple),index1(numericsimple),length(numericsimple);
if length<5
then raiseruntimeerror("計算期別請超過五期");

value1=linearregslope(price,length);
value2=linearregslope(index1,length);

if value1>0 and value2<0
then deviate=-1
else
　　if value1<0 and value2>0
　　then deviate=1
　　else
　　　　deviate=0;
```

透過這個函數，我們就可以比較兩個數列，看看他們是否出現背離的情況，這個函數的寫法是當第一個值下跌而第二個值上昇時，函數的回傳值是 1 ，相反的則是-1 ，若兩個值同方向則是傳回0。

假設我們想找出收盤價下跌，但跟10日RSI上昇的股票，我們就可以運用這個函數，寫出下面這樣的腳本

```xs
value1=rsi(close,10);
if deviate(close,value1,10)=1
then ret=1;
```

有了這個函數，我們就可以隨時把兩個時間序列拿出來檢定是否有背離的現象了。

今天就為大家介紹到此，有任何建議的話歡迎隨時跟我們講喔。下次見。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="私房交易策略之：強勢股整理結束"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="私房交易策略之：強勢股整理結束"></a>
# [私房交易策略之：強勢股整理結束](https://www.xq.com.tw/lesson/xspractice/%E7%A7%81%E6%88%BF%E4%BA%A4%E6%98%93%E7%AD%96%E7%95%A5%E4%B9%8B%E5%BC%B7%E5%8B%A2%E8%82%A1%E6%95%B4%E7%90%86%E7%B5%90%E6%9D%9F/)

這樣的心情，如果用波浪理論來解釋，那就是在初昇段來不及參與之後，希望在主昇段發動前夕，來得及坐上車。

為了尋找這一類的股票，我試著寫一個腳本，這個腳本有三個步驟

1.尋找特定日期以來漲幅最大，走勢最強的股票

2.這樣的股票，從這段時間以來的最高點到前一日的收盤，已修正一定的幅度

3.今天盤中該個股明顯走強。

在要寫這個腳本時，首先得先找出某特定日到最新的收盤價，一共是幾根Bar。這個函數就叫做GetBarOffset。使用的方法是傳入某個日期，這個函數就會回傳從現在到該日期共隔了幾根Bar。

```xs
Value1 = GetBarOffset(20151014);
```

如果今天是2015年10月15日，那上面的程式執行時Value1就會是1，代表是前一根bar。

有了這個函數，我就來寫出上面所提到的這個腳本

```xs
input: stardate(20150824);
input:ratio(30);
input:ratio1(7);
input:ratio2(2);
setinputname(1,"輸入上漲起始日");
setinputname(2,"輸入上漲最低幅度");
setinputname(3,"輸入最小拉回幅度");
setinputname(4,"今日最低漲幅");
setfirstbardate(stardate);
value1=getbaroffset(stardate);//找出輸入的日期是在第幾根bar
value2=highest(high[1],value1+1);//找出這一波的最高點

condition1=false;
condition2=false;

if value2>=close[value1]*(1+ratio/100)//計算波段漲幅有沒有符合要求
then condition1=true;

if value2>=close[1]*(1+ratio1/100)//計算拉回的幅度夠不夠要求
then condition2=true;

if nthhighestbar(1,high,10)>=5//從最高點到今天超過五根bar
then begin
if condition1 and condition2 and close>=close[1]*(1+ratio2/100)
then ret=1;
end;
```

在解釋這個腳本之前，我們先來看一下大盤最近的走勢

所以我上面寫的這個腳本，就是找出符合下面四個條件的股票

1.從8/24日以來，到最高點漲幅超過30%的股票

2.從最高點到前一個交易日的低點修正幅度超過7%

3.今天收盤比前一天收盤漲超過2%

4.從高點拉回整理超過五天。

這樣的腳本要尋找的，就是整理結束後的強勢股。

下面這張圖就是這個腳本今天跑出來的股票

當然不是每檔股票都會在初昇段之後就一定有主昇段，也不見得每檔股票都是休息了幾天後就開始再往上攻，不過這個策略的好處是，對於那些從特定日期以來走勢很強的股票，一旦整理結束，我們在第一時間就會收到電腦的通知，這樣的好處是我不必一直盯著強勢股等拉回結束。

這個腳本可以名列我的私房腳本第二名，介紹給大家，至於日期及漲幅，拉回幅度，這些參數就請大家自己調整囉! |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="用XS寫籌碼集中度的選股策略"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="用XS寫籌碼集中度的選股策略"></a>
# [用XS寫籌碼集中度的選股策略](https://www.xq.com.tw/lesson/xspractice/%E7%94%A8xs%E5%AF%AB%E7%B1%8C%E7%A2%BC%E9%9B%86%E4%B8%AD%E5%BA%A6%E7%9A%84%E9%81%B8%E8%82%A1%E7%AD%96%E7%95%A5/)

首先，我們設計一個計算籌碼集中度的自訂函數，把他叫做BS，取Buysell(買賣超)的簡寫。不知道如何設定自訂函數的使用者，可以先複習一下計算區間漲跌幅的自訂函數這一篇文章：

```xs
input:days(numericsimple,"計算天數");
value1=GetField("主力買賣超張數");
value2=summation(volume,days);
value3=summation(value1,days);
if value2<>0
then value4=value3/value2*100;
bs=value4;
接下來我用這個函數寫了下面這個選股腳本:
input:days(10,"天數");
input:ratio(5,"最低百分比");
input:percent(10,"漲幅上限");
value1=bs(days);
if close[days-1]<>0 and close>close[days-1]
then value2=(close-close[days-1])/close[days-1]*100;
if value1>ratio and value2<percent
then ret=1;

outputfield(1,value1,0,"籌碼集中度");
outputfield(2,value2,1,"區間漲幅%");
```

這個腳本可以找出在N日內，籌碼集中度高於多少百分比，且漲幅低於多少百分比的股票

這個腳本來跑當天的資料，挑出的股票如下圖

如果想要更改區間，只要點選下圖的調整參數按鈕

就會跳出以下的視窗，調整不同的天期或百分比了

在XS內還有非常多跟籌碼相關的欄位，大家有空的時候可以到這裡來查詢。如果對於欄位的定義不清楚的話，可以試著用OutputField的方式來把欄位數值列印出來檢視，或是隨時跟我們討論喔。下次見囉。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="那些股票中長紅之後還會續漲?"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="那些股票中長紅之後還會續漲?"></a>
# [那些股票中長紅之後還會續漲?](https://www.xq.com.tw/lesson/xspractice/%E9%82%A3%E4%BA%9B%E8%82%A1%E7%A5%A8%E4%B8%AD%E9%95%B7%E7%B4%85%E4%B9%8B%E5%BE%8C%E9%82%84%E6%9C%83%E7%BA%8C%E6%BC%B2/)

有的盤中追漲停，有的開盤追今天最強的，有的尾盤追第一根長紅棒，今天要跟大家分享的，就是如果您在尾盤追中長紅的股票，隔天怎麼判斷這檔股票會續強還是得快落跑。
下圖是前天漲幅超過3%且今天成交量超過1000張的股票

從這張圖我們可以發現，前一天中長紅之後，隔天還能續強的，基本上大致有兩個共同的特色
成交量比前一日增加 外盤追高的比例較高這兩點其實用市場常用的概念，就是在前一天拉高後，我們要觀察的就是:
後續追高的意願強不強。 拉上來之後，往下倒的力道大不大。基於這種觀察，我們可以透過XS的一個選股腳本，以及一個策略雷達腳本，組合成一個跨頻率的交易策略。

首先，我們可以先用日線找出前一日漲幅超過3%且成交量超過500張的股票

我們可以寫一個選股腳本如下:

```xs
if close>=close[1]*1.03 and volume>500
then ret=1;
```

如此一來，我們可以如下圖般，在選股中心新增一個選股叫前一日中長紅，然後加入排程，讓電腦每天幫我們找到符合條件的股票。

然後接下來，我們要寫一個腳本，在盤中來確認那些股票成交量有比前一天高，

XS有一個估計量的報價欄位，我們可以利用這個欄位寫一個策略雷達來找出今天預估量會超過昨天成交量且外盤量比內盤量大的股票，寫法很簡單如下：

```xs
value1=q_InSize;//當日內盤量
value2=q_OutSize;//當日外盤量
if GetQuote("估計量") > volume[1]
and value2>value1
then ret=1;
```

接下來，我們來把選股腳本跟策略雷達串在一起

請參考下圖

我們透過選股中心，每天找出"前一日中長紅"的股票，然後新增一個策略雷達，用上面所寫的這個"預估量超過昨天成交量"這個腳本，然後在設定指定範圍時，按下最右方的選股，然後在"挑選選股策略"這個跳出來的視窗中，選擇"前一日中長紅"的股票，這樣就可以在盤中找出前一天中長紅，今天成交量會比昨天大且外盤量比內盤量大的股票了。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="計算區間漲跌幅的自訂函數"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="計算區間漲跌幅的自訂函數"></a>
# [計算區間漲跌幅的自訂函數](https://www.xq.com.tw/lesson/xspractice/%E8%A8%88%E7%AE%97%E5%8D%80%E9%96%93%E6%BC%B2%E8%B7%8C%E5%B9%85%E7%9A%84%E8%87%AA%E8%A8%82%E5%87%BD%E6%95%B8/)

在設計XS的腳本時，可能會遇到一些在其他腳本內也會引用到的邏輯，例如我可能在好幾種選股法內都會想要檢查某個區間的漲跌幅，或者我可能在策略雷達內也想要依照某個區間的漲跌幅來做篩選。當腦袋裡面有這樣子常用的邏輯出現時，我們就可以試著把這段邏輯寫成一個XS的自訂函數，然後在不同的腳本內去引用這個函數，以後如果這個邏輯有需要調整的話，就只需要修改自訂函數的內容，不需要每個腳本都還要去調整。

撰寫自訂函數的方式跟撰寫腳本沒有很大的差別。首先從XScript編輯器內，點選檔案/新增，這時候要記得要選函數這個選項，然後輸入一個名稱，這個名稱必須是英文的。另外在此時也要決定這個函數做完之後要回傳什麼樣子的資料給使用函數的腳本。

以計算區間漲跌幅這個函數而言，我們希望算出來的數值就是漲跌幅，所以我們就選擇「數值」，表示這是一個數字。其他的選項還包含字串，或是邏輯值(True/False)。

設定好了之後，按確認，接下來就跟寫其他腳本一樣可以開始寫函數了。

```xs

 input:price(numeric);
 input:startday(numeric);
 input:endday(numeric);

 value1=getbaroffset(startday);
 value2=getbaroffset(endday);

 value3=(price[value2]/price[value1])-1 ;
 rangechange=value3*100;
```

有幾個撰寫自訂函數腳本時，要稍加留意之處：
自訂函數的名稱一定要是英文的， 在訂定函數內有一個特殊的變數，他的名字就是這個函數的英文名稱。如果計算完成之後要把數值回傳的話，必須把數值傳給這個變數。注意看上面這個例子內的這一行裡面的rangechange就是這個函數的名字，所以這一行表示這個函數算完之後的回傳值就是value3 * 100：

```xs
rangechange=value3*100;
```

自訂函數一樣可以使用input語法來設定參數。可是參數名稱後面括號後不能直接打預設值，而是要寫這個參數的資料格式：如果是數值的話，就寫Numeric，如果是字串的話，就寫String，如果是邏輯值的話，就寫TrueFalse。其他還有更複雜的傳遞格式，就容我們在其他文章內再為大家介紹。
有了這個函數，要寫區間漲跌幅的選股腳本就容易多了:

```xs
input:startday(20150702,"區間起始日");
input:endday(20151002,"區間結束日");
input:ratio(10,"最低漲幅");

value1=rangechange(close,startday,endday);
if value1>=ratio
then ret=1;

value2=GetField("最新股本");
value3=GetField("月營收年增率","M");
value4=GetField("股價淨值比","D");
outputfield(1,value1,1,"區間漲跌幅");
outputfield(2,value2,0,"股本(億)");
outputfield(3,value3,1,"月營收年增率");
outputfield(4,value4,1,"股價淨值比");

```

除了計算區間漲跌幅之外，在這個腳本內我們也使用了outputfield的方式，把我們想要觀察的欄位都印出來，方便一起檢視。

根據這個選股腳本，從今年一月三日到昨天，漲幅超過三成的股票如下:

一共35檔股票，從這些檔位中可以發現：

1. 股本除了中鴻144億之外，全部都在40億以下，30億以上的也只有鎧勝及揚智兩家。

2. 接近三分之二的股票月營收年增率有兩位數成長

3. 其中一半股價淨值比在二以下。

有了區間漲跌幅這個自訂函數，以及outputfield這個輸出特定資料欄位的語法，我們可以很快的挑出特定區間漲幅或跌幅超過一定比例的股票，並且以我們希望呈現的欄位，加以排序。這對我們在作功課時，有不少的幫助。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="選股欄位放大鏡：談OutputField跟GetFieldDate這兩個函數"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="選股欄位放大鏡：談OutputField跟GetFieldDate這兩個函數"></a>
# [選股欄位放大鏡：談OutputField跟GetFieldDate這兩個函數](https://www.xq.com.tw/lesson/xspractice/%E9%81%B8%E8%82%A1%E6%AC%84%E4%BD%8D%E6%94%BE%E5%A4%A7%E9%8F%A1%EF%BC%9A%E8%AB%87outputfield%E8%B7%9Fgetfielddate%E9%80%99%E5%85%A9%E5%80%8B%E5%87%BD%E6%95%B8/)

首先要介紹的是OutputField這個語法。

OutputField這個語法的主要功能，就是讓使用者可以在選股的結果畫面內增加想要顯示的欄位，欄位的內容可以是任意的數值，或是字串。所以除了可以拿來顯示商品的資料欄位之外，當然也可以拿來檢查腳本的執行狀態。

比方說我想要找EMA多頭排列的股票，可是怎麼找出來的怪怪的呢？這時候OutputField就派上用場了：

程式碼內第12行到第16行是小編為了確認執行結果時寫上去了。透過OutputField的語法，小編把這一檔商品是否符合(Condition1)以及均線的數值(Value1, Value2, Value3)都列印出來。特別注意到第12行的ret=1。這一行等於是把所有的股票都選出來了，這樣子的話才好檢查資料是不是正確啊。

底下是跑出來的結果：

有沒有看到紫色框線內的這四個欄位！這時候我們就可以利用排序的方式，甚至把結果匯出到Excel，然後再來做更深入的檢視。

除了腳本邏輯的問題之外，在設計選股方式時，小編也常常遇到需要計算財報/籌碼等欄位資料的需求。由於這些資料的公佈時間不一，而且單位也比較複雜(有的是億，有的是百萬，有的是百分比)，所以在確認腳本的執行邏輯前，小編通常也會利用OutputField把欄位印出來先看過一遍，再來決定腳本怎麼寫比較對。

例如說我想要估算最新一季的季營收時該怎麼寫？先把資料印出來再說：

```xs
Value1 = GetField("月營收","M");

ret = 1;
outputfield(1, GetFieldDate("月營收","M"), "最新月份");
outputfield(2, Value1);
outputfield(3, Value1[1]);
outputfield(4, Value1[2]);
```

透過OutputField把最近三期的月營收資料印出來，另外也透過GetFieldDate這個函數把最新一期月營收的資料日期印出來。

一樣的，我們還是利用ret=1，讓所有的商品都符合。一旦對資料的內容清楚之後，接下來我們就可以開始撰寫腳本的邏輯了。

我們想要估算季營收的方式是這樣子的：
同一季內如果已經公佈營收的，就用公佈的數值， 如果還沒有公佈的，就假設下個月的營收跟最新一期的營收是一樣的這樣子單然是很簡單的估算方式，大家可以依照自己的經驗再做調整。底下就是估算季營收的程式碼，在這裡GetFieldDate就派上用場了:

```xs
Var: mm(0);

Value1 = getfield("月營收","M");
mm = Month(GetFieldDate("月營收","M"));

if mm=1 or mm=4 or mm=7 or mm=10
then value2=Value1 * 3;

if mm=2 or mm=5 or mm=8 or mm=11
then value2=Value1 * 2 + Value1[1];

if mm=3 or mm=6 or mm=9 or mm=12
then value2=Value1+Value1[1]+Value1[2];

// 預估獲利(單位=百萬) = 季營收 * 毛利率 - 營業費用
//
value3 = value2 * GetField("營業毛利率","Q") - GetField("營業費用","Q");

OutputField(1, mm, "營收月份");
OutputField(2, Value1, "本月");
OutputField(3, Value1[1], "本月[1]");
OutputField(4, Value1[2], "本月[2]");
OutputField(5, value2, "預估單季營收(億)");
OutputField(6, value3 / 100, "預估單季本業獲利(億)");

ret = 1;
```

這段程式碼有點長，基本上就是運用GetFieldDate回傳的營收月份來決定該怎麼樣估算這一季的營收，然後在利用最新一期財報上的毛利率跟營業費用來估算是否會獲利。有興趣的朋友可以用這個腳本跑看看，記得要把執行的頻率設定成"月"喔。

在這篇文章內小編為大家介紹了OutputField，以及GetFieldDate這兩個函數。再搭配上GetField這個函數的話，相信大家在遇到選股方面的臭蟲時，應該知道有哪些武器可以運用了吧！ |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="如何運用Print指令來抓程式的臭蟲"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="如何運用Print指令來抓程式的臭蟲"></a>
# [如何運用Print指令來抓程式的臭蟲](https://www.xq.com.tw/lesson/xspractice/%E5%A6%82%E4%BD%95%E9%81%8B%E7%94%A8print%E6%8C%87%E4%BB%A4%E4%BE%86%E6%8A%93%E7%A8%8B%E5%BC%8F%E7%9A%84%E8%87%AD%E8%9F%B2/)

首先來介紹基本的語法：

```xs
Value1 = Average(Close, 5);
Print("Date=", Date, "Close=", Close, "Value1", Value1);
```

在Print指令內，我們可以把想要印出來的欄位一股腦的寫在裡面，可以印出字串，也可以印出數值。

然後當腳本執行時，在XScript編輯器內的下方的「執行」視窗內，就可以即時看到印出來的數值。在這裡可以同時看到不同腳本執行時印出來的內容，商品欄位顯示的是指標目前的商品，或是策略雷達執行的商品，腳本就是有使用到Print指定的腳本名稱：

有了這個指令之後，以後如果腳本跑出來的結果跟你想的不一樣的話，那就直接在腳本內加上Print指令，把想要看到的數值印出來就方便許多了！

Print的結果，除了在XScript編輯器內可以看到之外，另外在XS的安裝目錄底下也會產生檔案，檔案的路徑是在C:\SysJust\XQ2005\XS\Print底下，檔案的名稱，預設是腳本名稱_商品名稱.log:

如果使用到Print的腳本很多的話，那這個目錄內就會越來越亂。有辦法控制Print產出的檔案的位置或是名稱嗎？

密技1：指定輸出檔案路徑
在Print指令的第一個參數位置，加上file(“檔案路徑")的參數，就可以指定print出來的檔案要放置的路徑。例如，我們想輸出成交價至C:\Print下，可以這樣寫：

```xs
print(file("C:\print\"),date,symbol,close);
```

這個腳本輸出的檔案就會移到C:\Print下。

密技2：指定輸出檔案名稱
不過，既然可以指定輸出檔案路徑，當然也可以指定輸出的檔案名稱。一樣是透過file這個參數來指定輸出的檔案名稱。

例如，我們想把檔案名稱改為“商品代碼.log”，可以這樣寫：

```xs
print(file("c:\Print\[Symbol].log"),date,symbol,close);

```

有沒有注意到我們上一個範例怎麼指定商品代碼的？用[Symbol]。只要是在file裡面寫了[Symbol]的位置，在輸出的時候就會自動用當時的商品代碼來取代。另外，你也可以使用[ScriptName]、[Freq]或[Date]。這些特殊字串在指定路徑或檔名的時候都可以用，這樣子我們可以輸出的變化就很多了。

如果我們想按商品代碼來整理數據，可以這樣寫：

```xs
print(file("C:\print\[Symbol]\"),date,symbol,close);
```

這樣子可以把不同腳本輸出的資料都集中到個別商品的目錄當中。

或者是，我們想把所有數據都輸出到同一個檔案print.log中，可以這樣寫：

```xs
print(file("C:\print\print.log"),date,symbol,close);

```

有了這些功能，我們就可以控制檔案輸出的位置，不管是要把執行結果存下來，或是跟其他程式共用檔案，都可以輕易的辦到。

小編要在這裡告訴大家一個好消息，在XQ操盤高手  2.40 改版中，Print指令已可在回測與選股中心使用！由於回測與選股中心Print指令是從雲端上運行，故使用此功能跑回測時，執行回測的時間會較久一些，此特性請大家注意一下囉！

回測或選股腳本撰寫好Print指令後，在回測設定畫面下方記得勾選「啟動腳本內Print指令」

在選股中心畫面右上角記得勾選「啟動腳本內Print指令」，即可在執行回測或選股策略後，將所需要檢查的數據輸出，方便查找問題的原因。

希望從此之後，大家都可以鎮定的面對臭蟲，Happy Coding and Happy Hunting。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="探討變數序列的觀念：幾天前黃金交叉商品為例"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="探討變數序列的觀念：幾天前黃金交叉商品為例"></a>
# [探討變數序列的觀念：幾天前黃金交叉商品為例](https://www.xq.com.tw/lesson/xspractice/%E5%A6%82%E4%BD%95%E6%89%BE%E5%87%BA%E5%B9%BE%E5%A4%A9%E5%89%8D%E9%BB%83%E9%87%91%E4%BA%A4%E5%8F%89%E7%9A%84%E5%95%86%E5%93%81%EF%BC%9A%E6%8E%A2%E8%A8%8E%E8%AE%8A%E6%95%B8%E5%BA%8F%E5%88%97%E7%9A%84/)

首先我們來看一下均線黃金交叉的腳本：

```xs
input: Shortlength(5, "短期均線期數");
input: Longlength(20, "長期均線期數");

Value1 = Average(Close,Shortlength);
Value2 = Average(Close,Longlength);

If Value1 cross over Value2 then ret = 1;
```

在這個腳本我們先計算短期的均線數字，把他存在Value1裡面，再計算長期的均線數字，把他存在Value2裡面，然後判斷Value1是否向上穿越(cross over)Value2，如果是的話則觸發。

從這段腳本內，我們只有看到Value1, Value2這兩個數字，他們代表的就是最新的短期均線跟長期均線的數值。可是，大家應該也知道，XS腳本在執行時是從左往右一根一根執行的，而這裡最重要的觀念是，在執行的過程內，每一根K棒執行時所計算過的數值，都是有存下來的。

要存取這些在之前K棒所算過的數值的方式，就跟存取前一根K棒的價格的方式一樣，是採用[1]的方式來取得。所以，如果要找到前一期黃金交叉的商品的話，只需要把判斷向上穿越的程式碼從Value1改成Value1[1]就可以了。

```xs
input: Shortlength(5, "短期均線期數");
input: Longlength(20, "長期均線期數");

Value1 = Average(Close,Shortlength);
Value2 = Average(Close,Longlength);

If Value1[1] cross over Value2[1] then ret = 1;
```

這樣子是不是很簡單呢？

最後跟大家做個整理：在腳本內的每一個計算後的數值，例如Value1, Value2，或是其他你自己命名的變數，
其實都是像Open，High，Low，Close一樣是一個時間序列。而這個變數的前一筆的數值，就是腳本在上一根K棒執行時所產生的數值。

再來看一個例子：

```xs
Value1 = Close - Close[1];
if Value1 > 0 And Value1[1] > 0 And Value1[2] > 0 then ret=1;
```

大家可以看得出這個腳本要找的是什麼商品了嗎？就祝大家作多的股票都跟這個腳本一樣，連續上漲三天喔！！ |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="SetBackBar指定頻率設定資料筆數"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="SetBackBar指定頻率設定資料筆數"></a>
# [SetBackBar指定頻率設定資料筆數](https://www.xq.com.tw/lesson/xspractice/%E8%A8%AD%E7%BD%AE%E6%8C%87%E5%AE%9A%E9%A0%BB%E7%8E%87%E5%BC%95%E7%94%A8%E7%AD%86%E6%95%B8%E7%9A%84%E6%87%89%E7%94%A8/)

```xs
//交易策略執行頻率請設定5分鐘
//
//進場條件
//收盤價在20日均線之上
//且 5 分鐘K棒突破過去三根K棒最高價
//
//出場條件
//收盤價在20日均線之下
//且 5 分鐘K棒跌破過去三根K棒最高價
input:Davglength(20,"日均線期數"), Hlength(3, "近N根5分K棒最高價");

settotalBar(Davglength*54);

condition1 = close > average(GetField("收盤價", "D"),Davglength);
condition2 = close cross Above highest(high[1], Hlength);
condition11= close < average(GetField("收盤價", "D"),Davglength);
condition21= close cross under highest(high[1], Hlength);

if condition1 and condition2 and filled = 0 then
setposition(1,market);

if condition11 and condition21 and filled <> 0 then
setposition(0,market);
```

上述範例在跑台股商品時，因為要用5分K棒組成日K棒來運算範例的20日均線數值，所以資料筆數需要設定到54(一天54根5分K)×20(均線期數)＝1080筆，也就是使用settotalBar(Davglength*54)這行語法或者在介面上設定足夠的資料筆數，才能成功執行交易或雷達策略。

如果資料筆數設定不足，會因為GetField指定日頻率資料筆數不足夠的關係，導致雷達或交易策略在洗價時發生錯誤，有關資料筆數的說明可以參考資料讀取範圍與腳本執行的關係此篇文章說明。

在v7.06.01版本中，此範例可以使用 setbackbar(20,"D"); 直接將GetField指定頻率的資料引用筆數設定充足，主頻率5分K只要設定三筆即可執行成功交易或雷達策略，如下範例交易腳本語法

```xs
//交易策略執行頻率請設定５分鐘
//
//進場條件
//收盤價在20日均線之上
//且 5 分鐘K棒突破過去三根K棒最高價
//
//出場條件
//收盤價在20日均線之下
//且 5 分鐘K棒跌破過去三根K棒最高價
input:Davglength(20,"日均線期數"), Hlength(3, "近N根5分K棒最高價");
settotalBar(Hlength);
setbarBack(Davglength,"D"); // 在 v7.06 版本添加 setbackbar 指定頻率調整資料引用筆數

condition1 = close > average(GetField("收盤價", "D"),Davglength);
condition2 = close cross Above highest(high[1], Hlength);
condition11= close < average(GetField("收盤價", "D"),Davglength);
condition21= close cross under highest(high[1], Hlength);

if condition1 and condition2 and filled = 0 then
setposition(1,market);

if condition11 and condition21 and filled <> 0 then
setposition(0,market);
```

有了setbackbar指定頻率設定資料引用筆數後，在此範例使用 setbarBack(Davglength,"D"); 來調整日頻率資料的引用筆數，就可以讓使用者簡便設定足夠的資料引用筆數，排除GetField指定頻率多期數運算時需要設定龐大資料筆數的困擾。

另外資料準備速度方面，可以發現運用SetBackBar設定指定頻率資料引用筆數比SetTotalBar方式還要快速，如下圖執行紀錄比較結果。值得注意的是，這張圖是單一商品與5分跨20日均線運算結果，若是多商品與跨更多日均線的運算，則資料準備速度差異會更大。

以上是Setbackbar指定頻率設定引用筆數相關應用介紹，有了Setbackbar指定頻率設定資料用筆數功能後，就能取代SetTotalBar設定龐大的資料筆數，加快交易策略或者雷達策略的資料準備運行速度，感謝大家的收看，咱們下次再會。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name="SetTotalBar資料讀取範圍與腳本執行的關係"></a>

[⬆ 回到索引](#📚-技術索引)

---

<a name="SetTotalBar資料讀取範圍與腳本執行的關係"></a>
# [SetTotalBar資料讀取範圍與腳本執行的關係](https://www.xq.com.tw/lesson/xspractice/%E8%B3%87%E6%96%99%E8%AE%80%E5%8F%96%E7%AF%84%E5%9C%8D%E8%88%87%E8%85%B3%E6%9C%AC%E5%9F%B7%E8%A1%8C%E7%9A%84%E9%97%9C%E4%BF%82/)

在XS語法入門內，我們有提到XS執行時的觀念是類似K線圖一樣，由左往右一筆一筆的執行。

而資料讀取範圍的這個參數，就是用來設定腳本要從哪裡開始執行起。如果你指定10筆的話，那系統就會提供最近10筆的K棒，然後從這10筆的第一根K棒(編號1)開始往右執行：

在上圖內藍色這個區塊就是資料讀取範圍，這個數值的定義根據不同腳本類型而異：
腳本類型資料讀取範圍指標指標總共要畫多少筆K棒，不包含當日即時的K棒範圍。 預設值是商品的全部資料長度。警示警示腳本要先執行多少筆之後開始進行即時的洗價以及是否要觸發。請注意這個範圍不包含當日即時的K棒。預設值是200。假設資料讀取範圍是10的話，那警示腳本會先執行當日之前的10根K棒，接下來執行當日即時的K棒，接下來等待收到即時資料後在執行時才會判斷是否要觸發。選股選股腳本要執行多少筆之後才判斷最後一根K棒是否觸發。 預設值是10。假設資料讀取範圍是10的話，則表示選股腳本總共要跑10筆資料，同時判斷第10筆資料是否ret=1。那我們設定資料讀取範圍時，到底該用什麼樣子的數值才對呢？這個問題沒有一個標準答案，必須依照腳本的內容來判斷。有一些指標，例如EMA均線，或是MACD指標，由於計算時會不斷的引用前一筆計算的數值做累計的平均，像這樣子的腳本，就會需要一個比較大的資料讀取筆數。反之，如果一個腳本內只會使用到最近固定期別的數字的話，那資料讀取筆數就不用設的很大。

資料讀取參數除了可以指定最近筆數之外，也可以指定一個固定的起始日期，這樣子可以方便使用者有更精準的控制。另外如果實際上這個商品並沒有那麼多筆資料的話，系統就會自動調整資料讀取筆數，以商品有的資料為基準。

最大引用範圍
執行腳本時，除了資料讀取筆數這個參數會影響腳本執行的筆數之外，另外由於腳本內常常需要讀取之前的資料來做計算，例如要計算3日收盤價平均值時，會引用到上一根K棒的收盤價以及上上一根K棒的收盤價：

```xs
Value1 = (Close + Close[1] + Close[2]) / 3;

```

所以在腳本要執行之前，系統除了要準備資料讀取範圍內的K棒資料之外，另外也需要往前多準備一些K棒資料：

在上面圖示內，資料讀取範圍左邊的這一個黃色區塊，就叫做最大引用範圍。這個範圍的目的是為了讓腳本可以正確的參考到目前執行的這根K棒往前的資料。同樣的，這個範圍的大小，會跟腳本內會參考到幾期前的資料有關。系統在開始執行時會提供一個預設值，如果在執行過程內腳本參考到了範圍之外的資料而發生錯誤的話，系統還是會繼續執行，改由下一根K棒開始重新計算。

在下面這張圖內，假如腳本的內容是

```xs
Value1 = Close - Close[5]; {計算５日漲幅}

```

由於最大引用筆數只有4筆，所以腳本要去讀取Close[5]的時候會發生錯誤。這時候系統就會重新調整執行的範圍，從發生錯誤的下一筆開始重新執行。

發生錯誤之後系統自動重新調整最大引用範圍

注意到系統會往右一筆重新執行，讓最大引用範圍變大，這樣子一來，執行的筆數會變少，可是就不會發生錯誤了。由於系統執行時會自動處理這種情形，使用者在大部分的情形底下可以完全忽略最大引用範圍這個參數。可是如果腳本的引用範圍很大的話，那則可以從腳本內透過函數的呼叫來告訴系統這個參數，已減少執行時發生錯誤的機會。這部分在底下會有範例說明。

講完了基本的觀念之後，接下來我們來看不同類型的腳本的設定方式。

XS自訂指標
在技術分析圖形上面放入XS自訂指標時，目前系統預設指標腳本的資料讀取筆數就是這個商品的全部歷史資料，技術分析的設定畫面上也就不提供指定資料讀取範圍的參數，你只要把指標放到技術分析圖上面就對了！！

針對特殊的情境，可以在屬性設定的視窗，直接指定計算資料的長度或起始日。

或是可以透過以下的函數從腳本內控制指標資料的長度：

```xs
SetTotalBar(300);

```

如果在指標腳本內引用SetTotalBar(300)的話，那這個指標就只會畫最新的300筆資料(不包含當日即時的資料)。如果你希望可以從某個指定日期開始畫圖，可以使用這一版新提供的SetFirstBarDate函數：

```xs
SetFirstBarDate(20150101);

```

這樣子的話指標就會從20150101日開始畫圖。

至於最大引用範圍這個參數，系統會根據使用者指定的資料範圍來推算一個預設值。使用者也在腳本內透過SetBackBar這個函數來做控制：

```xs
SetBackBar(50);

```

另外還有一個 SetBarBack函數，他的用途跟SetBackBar是一模一樣的。

XS策略雷達
設定警示畫面內可以指定資料讀取筆數或是第一筆資料的日期：

進階的使用者也可以從腳本內直接透過 SetTotalBar 或是 SetFirstBarDate 這兩個函數來控制資料讀取筆數。同樣的，在腳本內也可以透過 SetBackBar函數來控制最大引用筆數。

XS選股
設定選股策略時可以指定資料讀取筆數或是第一筆資料的日期：

進階的使用者也可以從腳本內直接透過 SetTotalBar 或是 SetFirstBarDate 這兩個函數來控制資料讀取筆數。

至於最大引用筆數這個參數，在XS選股系統內由於執行時已經事先讀取了全部的資料，所以使用者不需要再做設定。

以上為大家介紹了資料讀取筆數的觀念，以及設定的方式。相關的函數請參考函數說明文件。有任何問題或是建議的話，也歡迎您隨時跟我們說喔。 |

---


[⬆ 回到索引](#📚-技術索引)

---

<a name=""></a>
# [](https://www.xq.com.tw/lesson/xspractice)

看完了XS語法入門跟XS實戰7門課之後，應該就有基本觀念可以開始使用XS了。

在這裡，我們會陸續透過一系列的範例來分享XS的應用技巧。 |

[⬆ 回到索引](#📚-技術索引)

[⬆ 回到索引](#📚-技術索引)
